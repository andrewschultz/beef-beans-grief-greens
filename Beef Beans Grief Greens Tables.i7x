Version 1/240113 of Beef Beans Grief Greens Tables by Andrew Schultz begins here.

"This lays out the major tables for Beef Beans Grief Greens for easy indexing and searching, as well as any rules related to the tables. It's mostly for tracking points."

volume the main thing

to say yrrr: say "you return[if player is in reeve row]ed[end if] to Reeve Row"

table of verb checks [the order of things to solve is roughly alphabetical, so the lurking lump always gives you the best value, but this is subverted by if one rhyme pushes the game/story further than the others. So Leave Lo is first. Sassed can be anywhere for the bonus point, since the lump explicitly avoids bonus points.]
w1 (text)	w2 (text)	posthom (topic)	hom-txt-rule (rule)	think-cue	okflip	core	idid	best-room	check-rule	run-rule	wfull (topic)	think-advice (text)
"sassed"	"ceased"	--	--	false	true	false	false	--	vc-sassed-ceased rule	vr-sassed-ceased rule	--	--
"leave"	"lo"	"low"	vh-leave-low rule	false	true	true	false	reeve row	vc-leave-lo rule	vr-leave-lo rule	--	--
"heave"	"ho"	"hoe"	vh-heave-hoe rule	false	true	true	false	reeve row	vc-heave-ho rule	vr-heave-ho rule	--	"You can [b]HEAVE HO[r] [once-now of vc-heave-ho rule] you're [here-in of reeve row] with someone strong enough to help you pull the rayed rug up."
"prune"	"pride"	"pried"	vh-prune-pried rule	false	true	true	false	reeve row	vc-prune-pride rule	vr-prune-pride rule	--	--
"spoon"	"spied"	--	--	false	true	true	false	reeve row	vc-spoon-spied rule	vr-spoon-spied rule	--	"You can get a [b]SPOON SPIED[r] [once-now of vc-spoon-spied rule] you have managed to consult the goon guide with a clear mind."
"believe"	"below"	--	--	false	true	true	false	reeve row	vc-believe-below rule	vr-believe-below rule	--	--
"played"	"plug"	--	--	false	true	true	false	reeve row	vc-played-plug rule	vr-played-plug rule	--	"You can find a [b]PLAYED PLUG[r] [once-now of vc-played-plug rule] [yrrr]."
"grieve"	"grow"	"greave"	vh-greave-grow rule	false	true	true	false	reeve row	vc-grieve-grow rule	vr-grieve-grow rule	--	--
"made|maid"	"mug"	--	--	false	true	true	false	reeve row	vc-made-mug rule	vr-made-mug rule	--	"You can find a [b]MADE MUG[r] [once-now of vc-made-mug rule] [yrrr]."
"jade"	"jug"	--	--	false	true	true	false	reeve row	vc-jade-jug rule	vr-jade-jug rule	--	"You can find a [b]JADE JUG[r] [once-now of vc-jade-jug rule] [yrrr]."
"laundering"	"lair"	--	--	false	true	true	false	wandering where	vc-laundering-lair rule	vr-laundering-lair rule	--	--
"squandering"	"square"	--	--	false	true	true	false	wandering where	vc-squandering-square rule	vr-squandering-square rule	--	--
"maundering"	"mare"	"mayor"	vh-maundering-mayor rule	false	true	true	false	wandering where	vc-maundering-mare rule	vr-maundering-mare rule	--	--
"pondering"	"pair"	"pare/pear"	vh-pondering-pare-pear rule	false	true	true	false	wandering where	vc-pondering-pair rule	vr-pondering-pair rule	--	--
"flopper"	"flea"	"flee"	vh-flopper-flee rule	false	true	true	false	wandering where	vc-flopper-flea rule	vr-flopper-flea rule	--	--
"chrome"	"craw"	--	--	false	true	false	false	wandering where	vc-chrome-craw rule	vr-chrome-craw rule	--	"You can open the [b]CHROME CRAW[r] [here-in of wandering] [if sides-visited >= 4]now[else]once[end if] you've explored everywhere you can aboveground."
"massive"	"mitt"	--	--	false	true	true	false	Ooh Ooh	vc-massive-mitt rule	vr-massive-mitt rule	--	--
"baller"	"bear"	"bawler/bare"	--	false	true	true	false	squalor square	vc-baller-bear rule	vr-baller-bear rule	--	--
"caller"	"care"	"collar"	--	false	true	true	false	squalor square	vc-caller-care rule	vr-caller-care rule	--	--
"dollar"	"dare"	--	--	false	true	true	false	squalor square	vc-dollar-dare rule	vr-dollar-dare rule	--	--
"neat"	"note"	--	--	false	true	true	false	squalor square	vc-neat-note rule	vr-neat-note rule	--	--
"meat"	"moat"	"meet/mote"	vh-meet-mote rule	false	true	true	false	squalor square	vc-meat-moat rule	vr-meat-moat rule	--	"You can find a [b]MEAT MOAT[r] [once-now of vc-meat-moat rule] you have found something more useful than the bleat bloat."
"beet"	"boat"	"beat"	vh-beat-boat rule	false	true	true	false	squalor square	vc-beet-boat rule	vr-beet-boat rule	--	"You can find a [b]BEET BOAT[r] [once-now of vc-beet-boat rule] you have found something more useful than the bleat bloat."
"found"	"fork"	--	--	false	true	true	false	squalor square	vc-found-fork rule	vr-found-fork rule	--	--
"fight"	"fires"	--	--	false	true	true	false	squalor square	vc-fight-fires rule	vr-fight-fires rule	--	--
"white"	"wires"	"wight"	vh-wight-wires rule	false	true	true	false	squalor square	vc-white-wires rule	vr-white-wires rule	--	"You can handle the [b]WHITE WIRES[r] [once-now of vc-white-wires rule] the lyres are safe to touch."
"lovin"	"lout"	--	vh-shovin-shout rule	false	true	true	false	dove n doubt	vc-lovin-lout rule	vr-lovin-lout rule	--	--
"shovin"	"shout"	--	--	false	true	true	false	dove n doubt	vc-shovin-shout rule	vr-shovin-shout rule	--	"You can make a [b]SHOVIN SHOUT[r] [once-now of vc-shovin-shout rule] you have the help of someone stronger."
"oven"	"out"	--	--	false	true	true	false	dove n doubt	vc-oven-out rule	vr-oven-out rule	--	"You can say [b]OVEN OUT[r] [once-now of vc-oven-out rule] you have someone strong enough to push the oven."
"hook"	"hard"	--	--	false	true	true	false	dove n doubt	vc-hook-hard rule	vr-hook-hard rule	--	--
"veiled"	"vases"	--	--	false	true	false	false	dove n doubt	vc-veiled-vases rule	vr-veiled-vases rule	--	--
"zap"	"zoo"	--	--	false	true	true	false	Gap Goo	vc-zap-zoo rule	vr-zap-zoo rule	--	--
"notice"	"knife"	--	--	false	true	true	false	Gap Goo	vc-notice-knife rule	vr-notice-knife rule	--	--
"carrot"	"cake"	--	--	false	true	true	false	Gap Goo	vc-carrot-cake rule	vr-carrot-cake rule	--	--
"see"	"soup"	"sea"	vh-sea-soup rule	false	true	true	false	gap goo	vc-see-soup rule	vr-see-soup rule	--	--
"dented"	"dials"	--	--	false	true	true	false	gap goo	vc-dented-dials rule	vr-dented-dials rule	--	--
"gain"	"garnish"	--	--	false	true	false	false	gap goo	vc-gain-garnish rule	vr-gain-garnish rule	--	--
"boo"	"boo"	--	--	false	true	true	false	ooh ooh	vc-booboo rule	vr-booboo rule	"booboo"	--
"coo"	"coo/koo"	--	--	false	true	true	false	ooh ooh	vc-cuckoo rule	vr-cuckoo rule	"cuckoo"	--
"doo"	"doo"	"do/dew"	--	false	true	true	false	ooh ooh	vc-doodoo rule	vr-doodoo rule	"doodoo"	--
"ju"	"ju"	--	--	false	true	true	false	ooh ooh	vc-juju rule	vr-juju rule	"juju"	--
"muu"	"muu"	"moo/moomoo"	vh-moomoo rule	false	true	true	false	ooh ooh	vc-muumuu rule	vr-muumuu rule	"mumu/muumuu"	--
"poo|pooh"	"poo|pooh"	--	--	false	true	true	false	ooh ooh	vc-poohpooh rule	vr-poohpooh rule	"poopoo/poohpooh"	--
"tu"	"tu"	--	--	false	true	true	false	ooh ooh	vc-tutu rule	vr-tutu rule	"tutu"	--
"book"	"bard"	"barred"	vh-book-barred rule	false	true	true	false	took tarred	vc-book-bard rule	vr-book-bard rule	--	"You can [b]BOOK BARD[r] [once-now of vc-book-bard rule] you've seen where the shook shard can take you."
"cook"	"card"	--	--	false	true	true	false	took tarred	vc-cook-card rule	vr-cook-card rule	--	"You can find a [b]COOK BARD[r] [once-now of vc-cook-card rule] you've seen where the shook shard can take you."
"look"	"lard"	--	--	false	true	true	false	took tarred	vc-look-lard rule	vr-look-lard rule	--	"You can exclaim [b]LOOK LARD[r] [once-now of vc-look-lard rule] you've seen where the shook shard can take you."
"pluck"	"plate"	--	--	false	true	true	false	stuck state	vc-pluck-plate rule	vr-pluck-plate rule	--	--
"penny"	"piles"	--	--	false	true	true	false	sty sto	vc-penny-piles rule	vr-penny-piles rule	--	--
"pie"	"po"	"pi/poe"	vh-pi-poe rule	false	true	true	false	sty sto	vc-pie-po rule	vr-pie-po rule	--	"You can buy a [b]PIE PO[r] [once-now of vc-pie-po rule] you have currency."
"veggie"	"ville"	--	--	false	true	true	false	edgy ill	vc-veggie-ville rule	vr-veggie-ville rule	--	"You can visit [b]VEGGIE VILLE[r] [once-now of vc-veggie-ville rule] the bopper bee isn't distracting you."
"soy"	"sauce"	"saws"	vh-soy-saws rule	false	true	true	false	toy toss	vc-soy-sauce rule	vr-soy-sauce rule	--	--
"pot"	"pourri"	--	--	false	true	false	false	slow slurry	vc-potpourri rule	vr-potpourri rule	"potpourri"	--
"honeyed"	"ham"	--	--	false	true	false	false	slow slurry	vc-honeyed-ham rule	vr-honeyed-ham rule	--	--
"grey|gray"	"gruel"	--	--	false	true	false	false	slow slurry	vc-grey-gruel rule	vr-grey-gruel rule	--	--
"just"	"jello"	--	--	false	true	false	false	slow slurry	vc-just-jello rule	vr-just-jello rule	"just jell o" or "just jello"	--
"scrappy"	"scrawl"	--	--	false	true	true	false	trappy trawl	vc-scrappy-scrawl rule	vr-scrappy-scrawl rule	--	--
"whappy"	"wall"	--	--	false	true	true	false	trappy trawl	vc-whappy-wall rule	vr-whappy-wall rule	--	"You can hit the [b]WHAPPY WALL[r] [once-now of vc-whappy-wall rule] you know where to whap the wall."
"lone"	"laura"	"loan"	vh-loan-laura rule	false	true	true	false	happy hall	vc-lone-laura rule	vr-lone-laura rule	--	--
"known"	"nora"	--	--	false	true	true	false	happy hall	vc-known-nora rule	vr-known-nora rule	--	--
"pappy"	"paul"	"pall"	vh-pappy-pall rule	false	true	true	false	happy hall	vc-pappy-paul rule	vr-pappy-paul rule	--	"You can summon [b]PAPPY PAUL[r] [once-now of vc-pappy-paul rule] you are in the main ritual room."
"sappy"	"saul"	"sol"	vh-sappy-sol rule	false	true	true	false	happy hall	vc-sappy-saul rule	vr-sappy-saul rule	--	"You can summon [b]SAPPY SAUL[r] [once-now of vc-sappy-saul rule] you are in the main ritual room."
"cappy"	"caul"	"call"	vh-cappy-call rule	false	true	true	false	happy hall	vc-cappy-caul rule	vr-cappy-caul rule	--	"You can ask for your [b]CAPPY CAUL[r] [once-now of vc-cappy-caul rule] everyone else is present for the ceremony."
"yappy"	"yall"	--	--	false	true	true	false	happy hall	vc-yappy-yall rule	vr-yappy-yall rule	--	"You can get everyone [b]YAPPY YALL[r] [once-now of vc-yappy-yall rule] preparations for the ceremony and feast are complete."
"bappy"	"ball"	"bawl"	vh-bappy-bawl rule	false	true	true	false	happy hall	vc-bappy-ball rule	vr-bappy-ball rule	--	"You can have everyone play [b]BAPPY BALL[r] [once-now of vc-bappy-ball rule] the feast is complete, to burn off calories."
"wheat"	"well"	--	--	false	true	true	false	compete compel	vc-wheat-well rule	vr-wheat-well rule	--	--
"feet"	"fell"	--	--	false	true	true	false	compete compel	vc-feet-fell rule	vr-feet-fell rule	--	"You can claim your [b]FEET FELL[r] [once-now of vc-feet-fell rule] you've envisioned somewhere safe to fall to."
"sheet"	"shell"	--	--	false	true	true	false	compete compel	vc-sheet-shell rule	vr-sheet-shell rule	--	--
"beat"	"bell"	"beet"	vh-beet-meat-end rule	false	true	true	false	compete compel	vc-beat-bell rule	vr-beat-bell rule	--	"You can [b]BEAT BELL[r] [once-now of vc-beat-bell rule] there is a (door)bell to beat, or reach before the person ringing it runs away."
"meet"	"mel"	"meat"	vh-beet-meat-end rule	false	true	true	false	compete compel	vc-meet-mel rule	vr-meet-mel rule	--	"You can [b]MEET MEL[r] [once-now of vc-meet-mel rule] you know someone is near."
"repeat"	"repel"	--	--	false	true	true	false	compete compel	vc-repeat-repel rule	vr-repeat-repel rule	--	"[final-judge-think of 0]."
"heat"	"hell"	--	--	false	true	true	false	compete compel	vc-heat-hell rule	vr-heat-hell rule	--	"[final-judge-think of -1]."
"sweet"	"swell"	"suite"	vh-suite-swell rule	false	true	true	false	compete compel	vc-sweet-swell rule	vr-sweet-swell rule	--	"[final-judge-think of 1]."

section goon guide

a goodrhyme rule (this is the vc-prune-pride rule):
	if goon guide is not touchable, unavailable;
	if sco-prune-pride is true:
		vcal "You're already humble enough. I mean, we could always be more humble, but there's overdoing it, too.";
		already-done;
	ready;

this is the vh-prune-pried rule:
	say "No, no food is stuck in the goon guide.";

this is the vr-prune-pride rule:
	now sco-prune-pride is true;
	say "You get over your rage at the goon guide allowing bad people to make food as tasty as good people could make. You realize you are not the judge of such things, and you should not be.[paragraph break]The goon guide will be there with further technical help when you need it, to help repair the oven. You have an idea of the exact parts that are missing now. You will be more observant in the future when you look around.";
	move passive pit to Ooh Ooh;
	move vented vials to Gap Goo;
	move Light Lyres to Squalor Square;
	move shook shard to Dove n Doubt;
	guide-tweak;

[note we don't have to worry below about if spoon-spied is true, since we prune pride first. Sadly, this means I can't share "You already found enough spoons! What is this, a showing for [i]The Room[r]?" ]

a goodrhyme rule (this is the vc-spoon-spied rule):
	if player does not have goon guide, unavailable;
	if sco-prune-pride is false:
		vcp "You'd love to find a bit more silverware, and maybe the goon guide would be just the thing. Except you can't lower yourself to read it. Yet.";
		not-yet;
	if goon guide is not examined:
		vcp "You try to spy spoons based on your general impressions of the goon guide, but ... well, I'm going to make you read it more closely now you've pruned your pride.";
		not-yet;
	ready;

this is the vr-spoon-spied rule:
	now sco-spoon-spied is true;
	say "Well, it is worth a try. Amazingly, on rereading the goon guide, you find tips on locating lost silverware. You missed it with the first read-through, probably because you weren't focused on finding it[if oven-fixed-yet is false].[paragraph break]And here you thought it would just help you fix [ap-ov][else]. Technical and subjective advice--the methods employed will help you figure how to find stuff you misplaced in general[end if]!";
	now player has spoons;
	guide-tweak;

to guide-tweak:
	decrease to-number of goon guide by 2755;
	if guide-score is 2:
		say "[line break]You flip through the goon guide once again. You seem to have gotten the big stuff from it. You tuck it in a safe place behind the oven for reference. You have enough to carry around.";
		moot goon guide;

to say ap-ov:
	say "[if oven is in reeve row]the oven[else]an appliance[end if]"

chapter all the time

a goodrhyme rule (this is the vc-sassed-ceased rule):
	if player does not have fast feast, unavailable;
	if sco-sassed-ceased is true:
		vcal "You already felt relief things settled down!";
		already-done;
	ready;

this is the vr-sassed-ceased rule:
	now sco-sassed-ceased is true;
	say "Yes, you're glad you were given this responsibility, and people snickered about how flaky you were.";

chapter reeve row scoring

section rayed rug flips

definition: a rhymable (called rh) is can-rug-rhyme:
	unless rh is carried, no;
	if guess-table of rh is table of rayed rug guesses and rh is not rayed rug, yes;
	no;

this is the aid-ugh rule:
	if rayed rug is not touchable and made mug is not touchable and jade jug is not touchable and played plug is not touchable, unavailable;

this is the rug-here rule:
	if rayed rug is not touchable:
		vcp "Looking at the [list of can-rug-rhyme rhymables] that came from the rayed rug, you realize Reeve Row would be a better place to try this.";
		not-yet;

a goodrhyme rule (this is the vc-played-plug rule):
	abide by the aid-ugh rule;
	if sco-played-plug is true:
		vcal "You already got a played plug!";
		already-done;
	abide by the rug-here rule;
	ready;

this is the vr-played-plug rule:
	now sco-played-plug is true;
	say "Ah, that's what the bump in the rayed rug is. You take it.";
	now player has played plug;
	oven-check;
	rhyme-rug-check 2804;

a goodrhyme rule (this is the vc-made-mug rule):
	abide by the aid-ugh rule;
	if sco-made-mug is true:
		vcal "You already have a made/maid mug for the meal.";
		already-done;
	abide by the rug-here rule;
	ready;

this is the vr-made-mug rule:
	now sco-made-mug is true;
	say "You find and take a made mug hidden in the rug.";
	now player has made mug;
	rhyme-rug-check 2703;

a goodrhyme rule (this is the vc-jade-jug rule):
	abide by the aid-ugh rule;
	if sco-jade-jug is true:
		vcal "You already have a jade jug for the meal.";
		already-done;
	abide by the rug-here rule;
	ready;

this is the vr-jade-jug rule:
	now sco-jade-jug is true;
	say "You find and take a jade jug hidden in the rug.";
	now player has jade jug;
	rhyme-rug-check 2703;

section reeve row general flips

a goodrhyme rule (this is the vc-believe-below rule):
	if sco-believe-below is true:
		vcal "You already believed in[if trappy trawl is visited], and took,[else if sco-heave-ho is true], and found,[end if] a passage down.";
		already-done;
	abide by the self rhymes check rule;
	ready;

this is the vr-believe-below rule:
	if sco-heave-ho is false:
		say "You believe something must be below. Below that rug, you'd guess. You might not be able to remove it by yourself. But you have faith now!";
	now sco-believe-below is true;
	declue-here-by 2855;
	process the Reeve Row check passage down rule;

a goodrhyme rule (this is the vc-leave-lo rule):
	if trappy trawl is visited:
		vcal "You've left Reeve Row for good.";
		already-done;
	if player is not in reeve row:
		vcal "But you already left Reeve Row!";
		already-done;
	ready;

this is the vh-leave-low rule:
	say "You're already feeling a bit low. You don't need to advertise it, or sink lower.[paragraph break]That has to be really, really close, though! Perhaps a potentially hearty interjection would work better.[paragraph break]How to leave with your head held high, with a forceful word showing you're willing to take on the world?";

this is the vr-leave-lo rule:
	if copper key is moot:
		say "This is ambiguous now you've opened a passage [b]DOWN[r] as well as [b]OUT[r].";
		the rule succeeds;
	if sco-leave-lo is false:
		say "Sometimes you just need the right time to say a simple phrase to get you going. And you do, here.";
		open-psg outside and wandering where;
	else:
		say "[i][bracket]You can just go [b]OUT[r][i] or [b]N/NORTH[r][i] for fewer keystrokes.[close bracket]";
	now sco-leave-lo is true;
	declue-here-by 2752;
	move player to Wandering Where;

a goodrhyme rule (this is the vc-heave-ho rule):
	if sco-heave-ho is true:
		vcal "There [if trappy trawl is visited]was[else]is[end if] nothing more to heave besides the rayed rug.";
		already-done;
	abide by the self rhymes check rule;
	if sco-oven-out is false:
		vcp "You try to pull the rayed rug, but it feels nailed down in enough places you can only look under a bit. You can't move it by yourself. You need a stronger assistant!";
		not-yet;
	ready;

this is the vh-heave-hoe rule:
	say "You are not in a garden. Try an interjection instead.";

this is the vr-heave-ho rule:
	now sco-heave-ho is true;
	say "You and [the lout] pull on one, two, THREE! It takes a couple of times, but the rug bursts free from the floor. You see [if sco-believe-below is true]nothing under the rug. You were sort of hoping for a new passage. Maybe you can do more than just hope.[paragraph break]Maybe there's a way to visualize a way down[else]an outline of the passage you believed was there[end if].[paragraph break]The lovin['] lout beams at such a display of strength! You smile back and nod vigorously. Then [the lout] claps you on the shoulder, smiles and shakes your hand before leaving.";
	moot lout;
	say "[line break]Your kneeling on the floor to pull the rug up gave you a good view inside the oven. There you find something called a goon guide. But you can't use it, because you're too good to be a goon. That's how it works, right? Or maybe not...";
	now player has goon guide;
	declue-here-by 2752;
	heave-ho-rug-check;
	general-rug-check;
	process the Reeve Row check passage down rule;

to general-rug-check:
	if rug-score is 3 and sco-heave-ho is true:
		say "[line break]You found something below the rug, and all its rays are dull. Best to tuck it away in a spot where it won't distract you.";
		moot rayed rug;

to heave-ho-rug-check:
	if rug-score < 3:
		say "While you've cast the rug aside, you'll keep an eye on it. You sense it has [if rug-score is 0]a lot[else if rug-score is 1]some[else]one thing[end if] more to offer.";

a goodrhyme rule (this is the vc-grieve-grow rule):
	if trappy trawl is visited:
		vcal "That is the end goal, here. You are close.";
		already-done;
	if sco-grieve-grow is true:
		vcal "Too much grief will undo the growing you did.";
		already-done;
	abide by the self rhymes check rule;
	ready;

this is the vh-greave-grow rule:
	say "This isn't a warrior quest, here. No need for armor.";

this is the vr-grieve-grow rule:
	now sco-grieve-grow is true;
	say "You reflect on things. You recognize too much of this is bad. All the same, it puts you in a mindset to do what you must do.";
	declue-here-by 2804;

chapter wandering where scoring

a goodrhyme rule (this is the vc-pondering-pair rule):
	if player is not in wandering where, unavailable;
	ready;

this is the vh-pondering-pare-pear rule:
	if the player's command includes "pear":
		say "Ooh! That would be a food, but you couldn't cut up something living.";
	else:
		say "Yes to thinking, no to disrupting thinking.";

this is the vr-pondering-pair rule:
	now sco-pondering-pair is true;
	declue-here-by 2954;
	wander Ooh Ooh;

a goodrhyme rule (this is the vc-squandering-square rule):
	if player is not in wandering where, unavailable;
	ready;

this is the vr-squandering-square rule:
	now sco-squandering-square is true;
	declue-here-by 3056;
	wander squalor square;

a goodrhyme rule (this is the vc-maundering-mare rule):
	if player is not in wandering where, unavailable;
	ready;

this is the vh-maundering-mayor rule:
	say "No, something sentient, not someone."

this is the vr-maundering-mare rule:
	now sco-maundering-mare is true;
	declue-here-by 3004;
	wander Gap Goo;

a goodrhyme rule (this is the vc-laundering-lair rule):
	if player is not in wandering where, unavailable;
	ready;

this is the vr-laundering-lair rule:
	now sco-laundering-lair is true;
	declue-here-by 3004;
	wander Dove N Doubt;

a goodrhyme rule (this is the vc-chrome-craw rule):
	if wandering where is unvisited, unavailable;
	if sco-chrome-craw is true:
		if trappy trawl is visited:
			vcal "The chrome craw cannot open a secret path here.";
		else:
			vcal "You already opened the chrome craw!";
		already-done;
	if trappy trawl is visited:
		vcal "You could have opened the chrome craw in [wandering], but it's too late now. You don't want to go back. Everyone is waiting.";
		already-done; [not-yet isn't applicable here since you can't ever make your way back, and unavailable shuts you off completely. "Not-redoable" might be more accurate.]
	if player is not in wandering where:
		vcp "Ooh! A passage somewhere new? That'd be interesting. But this isn't an [if mrlp is dome d'aww]accessible[else]open[end if] enough space.";
		not-yet;
	if sides-visited < 4:
		vcp "That seems like it could work nicely here, but you don't know your way around well enough yet.";
		not-yet;
	ready;

this is the vr-chrome-craw rule:
	now sco-chrome-craw is true;
	say "You hear a large creaking from the ground beneath, and lo and behold, a chrome craw rumbles up from the ground! It's a bit scary-looking, but you're curious to explore it. You can go [b]DOWN[r] into it.";
	move chrome craw to Wandering Where;
	now slow slurry is mapped below Wandering Where;
	now Wandering Where is mapped above slow slurry;

section bopper bee scoring

a goodrhyme rule (this is the vc-flopper-flea rule):
	if copper key is not touchable and bopper bee is not touchable, unavailable;
	if sco-flopper-flea is true:
		vcal "You already got rid of the bopper bee!";
		already-done;
	ready;

this is the vh-flopper-flee rule:
	say "You can't order the bee around, but maybe it's disguised as something else.";

this is the vr-flopper-flea rule:
	now sco-flopper-flea is true;
	say "Wait! Something about the bee looks funny! Yes, it is not really a bee, but a less aggressive animal in disguise. Just knowing it is a flopper flea makes you less scared. Without fear, your running around gets more strategic, until it gets exhausted chasing you.[paragraph break]It buzzes and mumbles off. You're not surprised that, as it does, it drops a copper key, which you pick up.";
	now player has copper key;
	moot bopper bee;

chapter ooh ooh scoring

a goodrhyme rule (this is the vc-booboo rule):
	if player is not in ooh ooh, unavailable;
	if sco-booboo is true:
		vcal "You already feigned a booboo.";
		already-done;
	ready;

this is the vr-booboo rule:
	now sco-booboo is true;
	say "Sometimes, you just have to complain about small hurts, physical or mental, and exaggerate them. As long as you get back to your regular life soon after, it's not that bad, really. So you and [zl] do.";
	process-cocoa 2653;

a goodrhyme rule (this is the vc-cuckoo rule):
	if player is not in ooh ooh, unavailable;
	if sco-cuckoo is true:
		vcal "It was almost too crazy the first time.";
		already-done;
	ready;

this is the vr-cuckoo rule:
	now sco-cuckoo is true;
	say "You discuss, in an entirely sane manner, Swiss clocks with birds in them, and the chorus of the Mackenzie Brothers['] [']80s novelty smash hit [i]Take Off[r].";
	process-cocoa 2653;

a goodrhyme rule (this is the vc-doodoo rule):
	if player is not in ooh ooh, unavailable;
	if sco-doodoo is true:
		vcal "The value of such a word or phrase drops quickly with each use.";
		already-done;
	ready;

this is the vr-doodoo rule:
	now sco-doodoo is true;
	say "You patrol the area for animal waste, also remarking on other interesting swear-word replacements.";
	process-cocoa 2653;

a goodrhyme rule (this is the vc-juju rule):
	if player is not in ooh ooh, unavailable;
	if sco-juju is true:
		vcal "You already fake-performed juju.";
		already-done;
	ready;

this is the vr-juju rule:
	now sco-juju is true;
	say "You find ways to perform juju, or pretend to.";
	process-cocoa 2602;

a goodrhyme rule (this is the vc-muumuu rule):
	if player is not in ooh ooh, unavailable;
	if sco-muumuu is true:
		vcal "You already pictured Loulou and Zuzu in muumuus.";
		already-done;
	ready;

this is the vh-moomoo rule:
	say "Cows aren't quite it, here. But you have the right sound(s).";

this is the vr-muumuu rule:
	now sco-muumuu is true;
	say "Loulou and Zuzu shrug a bit. Why not? They come in so many different colors.";
	process-cocoa 2653;

a goodrhyme rule (this is the vc-poohpooh rule):
	if player is not in ooh ooh, unavailable;
	if sco-poohpooh is true:
		vcal "There's overdoing things, you reckon.";
		already-done;
	ready;

this is the vr-poohpooh rule:
	now sco-poohpooh is true;
	say "You show Loulou and Zuzu ways to dismiss this little thought experiment.";
	process-cocoa 2704;

a goodrhyme rule (this is the vc-tutu rule):
	if player is not in ooh ooh, unavailable;
	if sco-tutu is true:
		vcal "Tutus get old fast as jokewear.";
		already-done;
	ready;

this is the vr-tutu rule:
	now sco-tutu is true;
	say "Loulou and Zuzu can't dance worth a dang, but that seems worth trying, not for the whole stale 'ha ha dude in a dress' joke but because it does seem to fit the area's general vibe.";
	process-cocoa 2602;

section passive pit

a goodrhyme rule (this is the vc-massive-mitt rule):
	if passive pit is not touchable, unavailable;
	if sco-massive-mitt is true:
		vcal "You already got the massive mitt from the passive pit!";
		already-done;
	ready;

this is the vr-massive-mitt rule:
	now sco-massive-mitt is true;
	say "Now that you know what to look for, you are patient enough to look through the passive pit. You sift through a phone book, a remote control, some matches, a paddleball, a thermos, and oil cans with gun holes in them before you uncover a massive mitt.";
	now player has massive mitt;

section squalor square scoring

to evaluate-stare:
	if stare-score is 3:
		declue-here;
	else if stare-score is 2:
		now to-number of squalor square is -2804;
		now from-number of squalor square is -2856;
		say "[line break]The staller stare blinks repeatedly. The weirdness you introduced was too much for it. It was ready to judge squalor, but not THIS. It closes its eyes and fades into the wall it was on. Perhaps it will find another place to oversee.[paragraph break]Sadly, it is replaced by an almost-as-distracting bleat-bloat.";
		move bleat bloat to squalor square;
		moot staller stare;
		repeat through table of verb checks:
			if there is no best-room entry, next;
			if best-room entry is not squalor square, next;
			if idid entry is true, next;
			if check-rule entry is vc-baller-bear rule or check-rule entry is vc-caller-care rule or check-rule entry is vc-dollar-dare rule:
				now core entry is false;
				break;
	else if stare-score is 1:
		say "[line break]The staller stare blinks. It seems to shake where its head would be. It's a bit baffled by what it saw. It rolls its eyes as if to say, any more of this, and I give up.";
		now to-number of squalor square is 5608;
		now to-number of staller stare is 5608;

a goodrhyme rule (this is the vc-baller-bear rule):
	if player is not in squalor square, unavailable;
	if sco-baller-bear is true:
		vcal "You already summoned a baller bear!";
		already-done;
	ready;

this is the vr-baller-bear rule:
	now sco-baller-bear is true;
	if stare-score < 3:
		say "The staller stare is visibly affected by the audacity of a performing animal, just performing for fun and not trying to outrage anyone. It tries to stare down this oddness, but bears ignore that stuff. The star blinks, feeling weakened.";
	else:
		say "You attune yourself to your surrounding, watching a baller bear juggling and riding a unicycle. It's worth the time to relax and enjoy.";
		note-staller-gone;
	evaluate-stare;

to note-staller-gone:
	say "You amuse yourself with how the staller stare would've reacted to THAT. A bit of harmless schadenfreude. You think. You hope."

a goodrhyme rule (this is the vc-caller-care rule):
	if player is not in squalor square, unavailable;
	if sco-caller-care is true:
		vcal "You already provided caller care!";
		already-done;
	ready;

this is the vr-caller-care rule:
	now sco-caller-care is true;
	if stare-score < 3:
		say "The staller stare is visibly affected by the audacity of someone caring about them. Its eyes bug out. Is this a joke! Then the eyes blink. No, it's not crying, of course it's not, it's the squalor.";
	else:
		say "With the staller stare gone, you leave a bit of hope and genuine solicitousness for the next lost soul who stumbles here. Unfortunately, in such a seedy place, people may look at it askance. 'Why? What's the angle?' But you have to try.";
		note-staller-gone;
	evaluate-stare;

a goodrhyme rule (this is the vc-dollar-dare rule):
	if player is not in squalor square, unavailable;
	if sco-dollar-dare is true:
		vcal "You already made light of a dollar dare.";
		already-done;
	ready;

this is the vr-dollar-dare rule:
	now sco-dollar-dare is true;
	say "You espy kids being kids, bragging and calling each other chicken. It's a rite of passage, really.";
	if stare-score < 3:
		say "[line break]The staller stare is visibly affected by the audacity of someone daring someone else to do ... well, THAT. For just a dollar. (Don't worry. Nothing crude. Just standard kid-dare stuff.) Its eyes bug out. Is this a joke? Then the eyes roll around, as if to say, it doesn't see why it bothers to feel nosy.";
	else:
		note-staller-gone;
	evaluate-stare;

section downed dork scoring

a goodrhyme rule (this is the vc-found-fork rule):
	if downed dork is not touchable, unavailable;
	if sco-found-fork is true:
		vcal "You and the dork already found the fork!";
		already-done;
	ready;

this is the vr-found-fork rule:
	now sco-found-fork is true;
	say "You tell the downed dork your plans and what you might be able to find together. They seem amenable. You both spend a lot of time looking for a fork, any old fork, but once you find one, you start uncovering others. Squalor Square is just full of litter. The forks are in good shape.";
	now player has forks;
	declue downed dork;
	check-dork-done;

section bleat bloat scoring

a goodrhyme rule (this is the vc-neat-note rule):
	unless meat moat is touchable or beet boat is touchable or bleat bloat is touchable or neat note is touchable, unavailable;
	if sco-neat-note is true:
		vcal "But you already discovered the neat note. Rediscovering it won't make it neater or help you find more stuff.";
		already-done;
	ready;

this is the vr-neat-note rule:
	now sco-neat-note is true;
	say "It seems silly to expect someone to just up and drop by with a neat note. But surprisingly, the bleat bloat dies out, and someone singularly unimpressive-looking walks in, slowly and without confidence.";
	say "'Uh, hi. People call me the downed dork. I guess I deserve it, for not getting back up off the ground after being called a silly name. Anyway, I find weird stuff, nothing important--just, stuff more with-it people have no use for. Thanks for getting rid of the staller stare. Let me know if I can do anything for you.'";
	say "[line break]You find this part touching, because, well, you've been called a dork too. Even after being given this mission. You assure the downed dork that they might just be the person you need to see.";
	now player has neat note;
	move downed dork to squalor square;
	say "Ah! Much better! With a neat note to distract you, the bleat bloat seems less ubiquitous. After you read the note for a few minutes, the bleat bloat departs, feeling unloved and unnoticed.";
	moot bleat bloat;

a goodrhyme rule (this is the vc-beet-boat rule):
	abide by the bloat-transform rule;
	if sco-beet-boat is true:
		vcal "One beet boat is quite enough!";
		already-done;
	abide by the be-square rule;
	ready;

this is the vh-beat-boat rule:
	say "There's no competition here, violent, friendly, racing or otherwise."

this is the vr-beet-boat rule:
	now sco-beet-boat is true;
	say "[a-o], where you are given a good-sized beet boat. He doesn't particularly like beets, and neither do his coworkers, but why let it go to waste?";
	now player has beet boat;
	check-dork-done;

a goodrhyme rule (this is the vc-meat-moat rule):
	abide by the bloat-transform rule;
	if sco-meat-moat is true:
		vcal "More than one meat moat would be overkill. And not just for the animals killed to make the meat.";
		already-done;
	abide by the be-square rule;
	ready;

this is the vh-meet-mote rule:
	say "You needn't make the acquaintance of anything minuscule. You need more sizable food portions."

this is the vr-meat-moat rule:
	now sco-meat-moat is true;
	say "[a-o], where indeed a small meat moat (heavy meat, light gravy) is discovered. It looks and smells clean. Apparently, it was part of an all-you-can-eat buffet, and the workers ran out of stomach space.";
	now player has meat moat;
	check-dork-done;

a goodrhyme rule (this is the vc-fight-fires rule):
	if light lyres are not touchable, unavailable;
	if sco-fight-fires is true:
		vcal "You already fought the fires!";
		already-done;
	ready;

this is the vr-fight-fires rule:
	now sco-fight-fires is true;
	say "What a good citizen you are! What a patron of the arts beyond giving money! You manage to stop the lyres from burning.[paragraph break]Perhaps some part of them that you can use is left, though.";

a goodrhyme rule (this is the vc-white-wires rule):
	if light lyres are not touchable, unavailable;
	if sco-fight-fires is false:
		vcp "Okay, the light lyres might have white wires, but they'd be white-hot!";
		not-yet;
	if sco-white-wires is true:
		vcal "You already got the white wires!";
		already-done;
	ready;

this is the vh-wight-wires rule:
	say "Summoning the undead is one way to remember those who passed on, but it's a high-risk one."

this is the vr-white-wires rule:
	now sco-white-wires is true;
	say "You see what to look for in the lyres. The white wires have to come in handy to fix the oven.";
	now player has white wires;

chapter Dove N Doubt scoring

a goodrhyme rule (this is the vc-lovin-lout rule):
	if player is not in dove n doubt and lovin lout is not touchable, unavailable;
	if sco-lovin-lout is true:
		vcal "The lovin['] lout already appeared to help you!";
		already-done;
	ready;

this is the vr-lovin-lout rule:
	now sco-lovin-lout is true;
	say "A lovin['] lout strolls sheepishly into view, not sure to explain whether they've been terribly loyal, or they didn't mean to do the stuff the bad people suggested they do, or else. You assure them that's over now, and you want someone who can help a bit. For what, you're not sure yet.[paragraph break]You take a closer look at the oven. It's missing a lot! You probably need to find bits and pieces to make it serviceable again.";
	move lout to Dove N Doubt;
	now to-number of Dove N Doubt is 2805;

a goodrhyme rule (this is the vc-shovin-shout rule):
	if player is not in dove n doubt and lovin lout is not touchable, unavailable;
	if sco-lovin-lout is false:
		vcp "That'd give a boost of energy, but a shout won't be enough to move the oven on your own.";
		not-yet;
	if sco-shovin-shout is true:
		vcal "You already shouted with the lout! It's time to [if sco-oven-out is true]look elsewhere[else]finish removing the oven[end if].";
		already-done;
	ready;

this is the vh-shovin-shout rule:
	let shovin-here be whether or not the player's command includes "shout";
	if the player's command includes "n":
		say "The shout comes before the [if shovin-here is true]shove. Well, the shovin['][else]right action, or its participle[end if].";
	else if shovin-here is true:
		say "Very, very close. You want two words, one of which would have an apostrophe at the end.";
	else:
		say "Shove is the right idea. But there's something that goes with shoving. Well, shovin[']."

this is the vr-shovin-shout rule:
	now sco-shovin-shout is true;
	say "You and the lovin['] lout get together and push the oven forward.[paragraph break]Something seems to crumble loose. Success! You can probably move the oven more ambitiously now.";
	now to-number of lovin lout is 2703;
	now to-number of Dove N Doubt is 2703;

a goodrhyme rule (this is the vc-oven-out rule):
	if player is not in dove n doubt and oven is not touchable, unavailable;
	if sco-lovin-lout is false:
		vcp "You can't move the oven out by yourself!";
		not-yet;
	if sco-shovin-shout is false:
		vcp "Before moving the oven out, you'll have to be sure you and the lout can push it a bit.";
		not-yet;
	if sco-oven-out is true:
		vcal "But you've already moved the oven back to Reeve Row!";
		already-done;
	ready;

this is the vr-oven-out rule:
	now sco-oven-out is true;
	say "With the right commands and pacing, you and [the lout] move the oven from its former position. The lout, very unloutishly indeed, asks if you need the oven to move anywhere. You defer all 'Oh, it'd be too much!' but after some nonsense back-and-forthing, you both realize that's wasted energy. You both move the oven back to Reeve Row pretty quickly. The oven is -- well, you sense it isn't ready to cook anything, but it's a lot easier to repair here.[paragraph break]The [lout] waits around, as if they suspect they could help you with one more thing. Very considerate indeed, for a lout!";
	declue-here;
	move oven to Reeve Row;
	move lovin lout to Reeve Row;
	drop-player-at Reeve Row;

a goodrhyme rule (this is the vc-veiled-vases rule):
	if player is not in dove n doubt and player does not have veiled vases, unavailable;
	if sco-veiled-vases is true:
		vcal "You already got enough veiled vases!";
		already-done;
	ready;

this is the vr-veiled-vases rule:
	now sco-veiled-vases is true;
	say "The failed faces, which were kind of stick-figureish, unwrap and tie themselves back into a spider web of sorts. The web thickens. Then the wall the faces were on hardens and becomes clearer. You have veiled vases!";
	now player has veiled vases;

section shook shard scoring

a goodrhyme rule (this is the vc-hook-hard rule):
	if shook shard is not in location of player and player does not have cook card and player is not in took tarred, unavailable;
	if player is in took tarred:
		vcal "You got hooked now. How to get unhooked?";
		already-done;
	if player has cook card:
		vcal "You got what you needed from the shard.";
		already-done;
	ready;

this is the vr-hook-hard rule:
	now sco-hook-hard is true;
	say "You grab the shard and feel pulled to somewhere new...";
	drop-player-at took tarred;
	moot shook shard;

chapter Gap Goo scoring

a goodrhyme rule (this is the vc-zap-zoo rule):
	if player is not in gap goo, unavailable;
	if sco-zap-zoo is true:
		vcal "But you already converted the goo to a zoo!";
		already-done;
	ready;

this is the vr-zap-zoo rule:
	now sco-zap-zoo is true;
	say "Yes, yes. Much better now. The terrain changes! So much appears: a ferret (fake,) a tree troop, and lotus life. They're all very nice and add local flavor, but perhaps you need to appreciate them right to achieve your own goals.";
	move ferret fake to gap goo;
	move tree troop to gap goo;
	move lotus life to gap goo;
	declue-here;
	try looking;

section items in Gap Goo

a goodrhyme rule (this is the vc-notice-knife rule):
	if lotus life is not touchable, unavailable;
	if sco-notice-knife is true:
		vcal "You already noticed the knife! There is nothing more.";
		already-done;
	ready;

this is the vr-notice-knife rule:
	now sco-notice-knife is true;
	say "Ah! Yes! The lotus life almost took over parts of the zoo. You had to look for the knife, but it is there. And once you do, you notice a whole bunch of knives. Wow, when this zoo blossoms, it blossoms.";
	now player has knives;

a goodrhyme rule (this is the vc-see-soup rule):
	if tree troop is not touchable, unavailable;
	if sco-see-soup is true:
		vcal "You already got the soup!";
		already-done;
	ready;

this is the vh-sea-soup rule:
	say "Well, you need a lot, but not a whole sea of ";
	if the player's command includes "soup":
		say "soup";
	else:
		say "liquid-ish food";
	say ".";

this is the vr-see-soup rule:
	now sco-see-soup is true;
	say "You nod to the tree troop. You see what you want. They are glad to give it to you.";
	now player has soup;

a goodrhyme rule (this is the vc-carrot-cake rule):
	if ferret fake is not touchable, unavailable;
	ready;

this is the vr-carrot-cake rule:
	now sco-carrot-cake is true;
	say "Splat! The ferret (fake) runs off to reveal an untouched carrot cake! Its generosity sure isn't fake.";
	now player has carrot cake;
	moot ferret;

a goodrhyme rule (this is the vc-dented-dials rule):
	if player is not in gap goo, unavailable;
	if sco-dented-dials is true:
		vcal "You already got the dented dials!";
		already-done;
	ready;

this is the vr-dented-dials rule:
	now sco-dented-dials is true;
	say "The vented vials seem useless. Dented dials? Less so. They transmogrify. You take them.";
	now player has dented dials;
	moot vented vials;

a goodrhyme rule (this is the vc-gain-garnish rule):
	if vain varnish is not touchable and garnish is not touchable, unavailable;
	if sco-gain-garnish is true:
		vcal "You only need so much garnish.";
		already-done;
	ready;

this is the vr-gain-garnish rule:
	now sco-gain-garnish is true;
	say "You know what would make a lot more sense in a wooded area than toxic old varnish? Garnish! Green stuff! Once you've thought about that, you find more garnish than you need, growing all around.";
	now player has garnish;

chapter Took Tarred scoring

to get-untarred:
	declue-here-by 2704;
	say "[line break]";
	if shard-score < 3:
		say "You feel the area's hold on you weaken.";
	else:
		say "You feel sucked out of whatever the shard held you in. As you come back to [dove], the shard shatters. You sense there's nothing more to do here, so you walk back to the center of the action.";
		drop-player-at Wandering Where;

this is the shardy rule:
	if shook shard is touchable:
		vcp "The shard is calling to you. You must grasp it and find where it leads before searching for anything that concrete. But you feel you're on the right path.";
		not-yet;
	if took tarred is visited and player is not in took tarred and cook card is not moot:
		vcal "You got what you needed from the shard.";
		already-done;
	if player is not in took tarred, unavailable;

a goodrhyme rule (this is the vc-book-bard rule):
	abide by the shardy rule;
	if sco-book-bard is true:
		vcal "You already booked a bard!";
		already-done;
	ready;

this is the vh-book-barred rule:
	say "It's worth fighting censorship, but here you need to encourage a different art form than reading.";

this is the vr-book-bard rule:
	now sco-book-bard is true;
	say "You wait. People drift by. One is a bard. You book them for the ritual ahead. They can use the business. You refer them to the gnome high council, who are a ways away. But they're willing to pay well.";
	get-untarred;

a goodrhyme rule (this is the vc-cook-card rule):
	abide by the shardy rule;
	if sco-cook-card is true:
		vcal "You already found a card with cooking tips!";
		already-done;
	ready;

this is the vr-cook-card rule:
	now sco-cook-card is true;
	say "Oh my goodness. Could it be ... is it ... the missing insert from the goon guide? What luck!";
	now player has cook card;
	get-untarred;

a goodrhyme rule (this is the vc-look-lard rule):
	abide by the shardy rule;
	if sco-look-lard is true:
		vcal "You're not sure why you need lard, but you're pretty sure you don't need too much of it.";
		already-done;
	ready;

this is the vr-look-lard rule:
	now sco-look-lard is true;
	say "Lard, man. You're still not sure if you really need it, even if the cookbooks say you should. You've never had the guts to rebel and just prepare that dish without lard. Now's not the time to start, with others relying on you. So you're glad you looked so diligently, and you're glad it turned up.";
	now player has lard;
	get-untarred;

chapter stuck state scoring

a goodrhyme rule (this is the vc-pluck-plate rule):
	if player is not in stuck state, unavailable;
	ready;

this is the vr-pluck-plate rule:
	now sco-pluck-plate is true;
	say "You find one plate, and it give you a greater morale boost than expected. All of a sudden, you start noticing things around you. Like more plates! And even more plates! Plain plates! Gaudy plates! You're so involved, you don't realize you've walked out of the fog back to ...";
	now player has plates;
	drop-player-at wandering where;

chapter Sty Sto scoring

a goodrhyme rule (this is the vc-penny-piles rule):
	if player is not in sty sto, unavailable;
	if sco-penny-piles is true:
		vcal "You already found enough penny piles to buy something!";
		already-done;
	ready;

this is the vr-penny-piles rule:
	now sco-penny-piles is true;
	say "Reading [isles] carefully, you recognize that it, in fact, covers an isle not far from here (there were so many isles in the book, one was bound to be close,) rumored to hold particularly small-scale wealth. You excuse yourself to go look for it. Bri-Bro doesn't want to let you go at first, because he isn't here to provide a rest area for loiterers.[paragraph break]But you offer some of your considerable inventory as collateral. It was getting kind of heavy, so the break is nice.[paragraph break]You come back having found the penny piles. Now you just need to figure what to buy with it. Bri-Bro isn't going to keep such cheap stuff on the menu.[paragraph break]Bri-Bro gives you your inventory back but does snatch [isles] away, just because he notices you ARE carrying a lot, and it'd be a shame to drop any of that lovely food. Oh, and there might be an island he can loot once he's finished the day's work. You wouldn't deny him his fun, would you?";
	now player has penny piles;
	moot isles;

a goodrhyme rule (this is the vc-pie-po rule):
	if player is not in sty sto, unavailable;
	if sco-penny-piles is false:
		vcp "Yes, that is the cheapest item not on the menu, but you don't even have the measly cash to buy it.";
		not-yet;
	ready;

this is the vh-pi-poe rule:
	let h1 be whether or not the player's command includes "pi";
	let h2 be whether or not the player's command includes "poe";
	if h1 is true and h2 is true:
		say "Double narrow misses with the words. But you must be close!";
	else if h1 is true:
		say "Pi times the radius squared, and you've got something.";
	else:
		say "A pie would hit the spot. But not the author--perhaps a descriptor. A slangy one.";

this is the vr-pie-po rule:
	now sco-pie-po is true;
	say "You realize you're kind of broke, and you figure out what the cheapest item on the menu might be. It looks surprisingly good, and it's large, too.[paragraph break]Bri-Bro whispers to you, 'So many people demand the house special. I give them something frou-frou, but it's not really what I enjoy cooking. Now I'll have to pretend to kick you out.' He pushes you out of the establishment, but you complain you don't know the way back. Fortunately, he helps you...";
	now player has pie po;
	moot penny piles;
	drop-player-at Wandering Where;

chapter toy toss scoring

a goodrhyme rule (this is the vc-soy-sauce rule):
	if player is not in toy toss, unavailable;
	ready;

this is the vh-soy-saws rule:
	say "Well, maybe, but you can't eat saws.";

this is the vr-soy-sauce rule:
	now sco-soy-sauce is true;
	say "[ross] nods. 'Here you go. Not just packets. A whole big bottle! Don't drink it all at once. Heh.'[paragraph break]You fail to laugh at this joke, and your punishment is to be whisked back from whence you came.[paragraph break]Which, to be honest, is a big help, because you were pretty sure you couldn't find the way back.";
	now player has soy sauce;
	drop-player-at Wandering Where;

chapter edgy ill scoring

a goodrhyme rule (this is the vc-veggie-ville rule):
	if player is not in edgy ill, unavailable;
	if sco-flopper-flea is false:
		vcp "That'd be somewhere to go, once you disposed of the bopper bee.";
		not-yet;
	ready;

this is the vr-veggie-ville rule:
	now sco-veggie-ville is true;
	say "You realize that the fruit and vegetable groups are sorely underrepresented--well, the greens, at least. It's time to fix that!";
	say "[line break]You know there will be many stores in the distance. So you begin your trek. On the way you ask the way to Veggie Ville. People are helpful.[paragraph break]Once you get there, you worry you have no money to pay. But when they see the [feast], they recognize what you are here for. Why, they have just the thing: veggies and fruits of irregular shapes, which get thrown out anyway!";
	say "[line break]They're impressed with what you've gotten so far. They deem you a worthy leader of the ceremony. They wish you luck and provide directions back...";
	now player has veggies;
	drop-player-at Wandering Where;

chapter slow slurry scoring

a goodrhyme rule (this is the vc-potpourri rule):
	if player is not in slow slurry, unavailable;
	if sco-potpourri is true:
		vcal "You have quite enough potpourri!";
		already-done;
	ready;

this is the vr-potpourri rule:
	now sco-potpourri is true;
	say "You realize what the smell is. Well, not the specific ingredients. But it has to be potpourri. Where is it leaking from? Following your nose leads you to a large canister. It might be too much for the feast, but it can be used elsewhere -- and maybe even last until the next feast.";
	now player has potpourri;

a goodrhyme rule (this is the vc-honeyed-ham rule):
	if moneyed maam is not in location of player and player does not have honeyed ham, unavailable;
	if sco-honeyed-ham is true:
		vcal "But you already got the honeyed ham!";
		already-done;
	ready;

this is the vr-honeyed-ham rule:
	now sco-honeyed-ham is true;
	say "She sniffs and hands you a honeyed ham. Her charity done for the day, she brushes you aside with a hand gesture and then walks off impatiently, herself.";
	now player has honeyed ham;
	moot moneyed maam;

a goodrhyme rule (this is the vc-grey-gruel rule):
	if fey fool is not in location of player and player does not have grey gruel, unavailable;
	if sco-grey-gruel is true:
		vcal "More gruel? Ugh!";
		already-done;
	ready;

this is the vr-grey-gruel rule:
	now sco-grey-gruel is true;
	say "The [fool] hands you some surprisingly not-cray gray gruel and snickers before running off. What kind of square would actually LIKE gruel?[paragraph break]But you want to be considerate about allergies, people trying to diet, people with vegan lifestyles, and so forth. It will be useful, you guess, as a base or paste.";
	now player has grey gruel;
	moot cray cruel fey fool;

a goodrhyme rule (this is the vc-just-jello rule):
	if fussed fellow is not in location of player and player does not have jello, unavailable;
	if sco-just-jello is true:
		vcal "There's such a thing as too much Jell-O.";
		already-done;
	ready;

this is the vr-just-jello rule:
	now sco-just-jello is true;
	say "The [fellow] seems relieved to hand you the Jell-O. It may be just Jell-O, but it's all different kinds of colors and flavors. Including pineapple! You remember when some smart-aleck in middle school said pineapple Jell-O would never be a thing. In fact, that smart-aleck snickered when you were chosen for the ceremony.";
	now player has just jello;
	moot fellow;

chapter Trappy Trawl scoring

a goodrhyme rule (this is the vc-scrappy-scrawl rule):
	abide by the trawl-not-hall rule;
	if sco-scrappy-scrawl is true:
		vcal "You already found the scrappy scrawl!";
		already-done;
	ready;

this is the vr-scrappy-scrawl rule:
	now sco-scrappy-scrawl is true;
	say "Ah! You had confidence you could find a scrappy scrawl, and you do. The instructions seem to make sense. You need to look to break open a passage where there is none, yet. An unexpectedly hollow place. Apparently, from the scrawl, one that makes a special sort of noise. Not quite a THWIP or a THWUP. So where is the weak spot?";
	now to-number of trappy trawl is 2804;

a goodrhyme rule (this is the vc-whappy-wall rule):
	abide by the trawl-not-hall rule;
	if sco-scrappy-scrawl is false:
		vcp "That could be helpful, but you have no indication where a wall might sound whappy. Well, not yet. You need to search for evidence.";
		not-yet;
	ready;

this is the vr-whappy-wall rule:
	now sco-whappy-wall is true;
	say "Whunk. Whunk. The wall makes the same noise, until ... WHAP! There it is! You tap at it a bit, and a passage opens up to ...";
	move player to Happy Hall;
	say "Servants await. You notify them of the food you have back in Reeve Row. You all cook everything up. It's laid out for the banquet, and in the muddle, you no longer feel like any of it is in your inventory. It is for the people. (In other words, you might as well pretend the individual items vanished, and you can focus on invitations and the ceremony.)";
	repeat with X running through carried feastitems:
		moot X;

chapter happy hall scoring

section room proper

a goodrhyme rule (this is the vc-pappy-paul rule):
	abide by the call-a-pal rule; [not-yet is here]
	if sco-pappy-paul is true:
		vcal "You already summoned Pappy Paul!";
		already-done;
	ready;

this is the vh-pappy-pall rule:
	say "You need a person, not a dark cloud over everything.";

this is the vr-pappy-paul rule:
	now sco-pappy-paul is true;
	say "Pappy Paul appears when summoned, looking almost a bit too serious. He apologizes for this, but you reassure him this isn't a comedy roast. He chuckles slightly--the best compliment a joke can get from him!";
	reduce-hall-if;
	hall-move Pappy Paul;

a goodrhyme rule (this is the vc-sappy-saul rule):
	abide by the call-a-pal rule; [not-yet is here]
	if sco-sappy-saul is true:
		vcal "You already summoned Sappy Saul!";
		already-done;
	ready;

this is the vh-sappy-sol rule:
	say "Hmm. The right track, but a too widely smiling sun might not bring the right vibe. Oh, it might scorch everyone to death, too.";

this is the vr-sappy-saul rule:
	now sco-sappy-saul is true;
	say "Sappy Saul seems almost too happy to be summoned, then apologetic for his display. You vaguely encourage him to be himself.";
	reduce-hall-if;
	hall-move Sappy Saul;

a goodrhyme rule (this is the vc-cappy-caul rule):
	abide by the ritual-stuff rule;
	if sco-cappy-caul is true:
		vcal "You already retrieved your ceremonial garb!";
		already-done;
	if hall-guest-score < 4:
		vcp "You can't wear the ceremonial gear until everyone's here!";
		not-yet;
	ready;

this is the vr-cappy-caul rule:
	now sco-cappy-caul is true;
	say "You retrieve your ceremonial garb. For the first rite, you sacrifice [feast]. There are other copies of it.[paragraph break]Now is the time to stimulate discussion.";
	moot fast feast;
	if sco-sassed-ceased is false, max-down;

this is the vh-cappy-call rule:
	say "You've called everyone you need. You need something physical.";

a goodrhyme rule (this is the vc-yappy-yall rule):
	abide by the ritual-stuff rule;
	if sco-cappy-caul is false:
		vcp "You need to retrieve your ceremonial garb first!";
		not-yet;
	if sco-yappy-yall is true:
		vcal "You already held a discussion. Now it is time for games.";
		already-done;
	ready;

this is the vr-yappy-yall rule:
	now sco-yappy-yall is true;
	say "Everyone has a say about the deceased. Whom they miss most, whom they missed more than they expected, whom they almost forgot.[paragraph break]As conversation dies down, you remember something worrisome[if feast is not examined], which would've nagged you even more if you'd read [feast][end if].[paragraph break]The game's title being [this-game], where are the beans? Not that beans make or break a meal.";
	wfak;
	say "A loud voice booms 'Aha! We were waiting for you to ask!' A line of dutiful deans comes in, each with their own bag of beautiful beans. They don't just look beautiful. They smell beautiful, and you know they'll taste beautiful.";
	wfak;
	say "'While we knew you'd be up to it, well ... it sort of felt unfair. Byu, Du, you know.' You nod appreciatively. The conversation takes a turn to odd yet enjoyable semantics and various puns. The meal starts in earnest. It and the chatter take  while to die down.";
	wfak;
	say "It's time for sport, now. But not any usual sport. Perhaps something with a slightly quirky name.";

a goodrhyme rule (this is the vc-bappy-ball rule):
	abide by the ritual-stuff rule;
	if sco-yappy-yall is false:
		vcp "Sport, eventually, to burn the calories off. But before that, discussion.";
		not-yet;
	ready;

this is the vh-bappy-bawl rule:
	say "Too noisy. And whatever gets bapped should be inanimate."

this is the vr-bappy-ball rule:
	now sco-bappy-ball is true;
	say "A long game of bappy-ball is played. Everyone is exhausted and hungry afterwards. They eat everything that is put in front of them. You doze off, and you are given a vision... one where you are forced to be more, or less, popular than your associate. Yes, 'associate' is the right word.";
	move player to Compete Compel

section own aura

a goodrhyme rule (this is the vc-lone-laura rule):
	if player is not in happy hall, unavailable; [not-yet is here]
	if sco-lone-laura is true:
		vcal "You already summoned Lone Laura!";
		already-done;
	ready;

this is the vh-loan-laura rule:
	say "Sort of, though at such a ceremony, people do not worry about past grievances or debts.";

this is the vr-lone-laura rule:
	now sco-lone-laura is true;
	say "Lone Laura appears! She waves semi-awkwardly, and you hem and haw about how you hope you aren't disturbing her. No, she just doesn't go in for small talk. She saves her social energy for important stuff, like this. You must have worked hard to find everything!";
	decrease to-number of own aura by 2705;
	hall-move Lone Laura;

a goodrhyme rule (this is the vc-known-nora rule):
	if player is not in happy hall, unavailable; [not-yet is here]
	if sco-known-nora is true:
		vcal "You already summoned Known Nora!";
		already-done;
	ready;

this is the vr-known-nora rule:
	now sco-known-nora is true;
	say "Known Nora appears! You're worried you won't know her, and you don't, but you know OF her, quickly recalling tales of her throughout the kingdom. When she greets you, you feel much more well-known than you have before. You briefly wonder if your successful quest is getting to your head, but you realize Nora's confidence is the sort that is shared and not shoved in others['] faces.";
	decrease to-number of own aura by 2754;
	hall-move Known Nora;

chapter Compete Compel scoring

a goodrhyme rule (this is the vc-wheat-well rule):
	if player is not in compete compel, unavailable;
	if sco-wheat-well is true:
		vcal "You already [if sco-feet-fell is true]got here[else]figured your destination[end if]!";
		already-done;
	ready;

this is the vr-wheat-well rule:
	now sco-wheat-well is true;
	say "Yes, the wheat well must be your destination. Now how to stumble across to it?";
	[now from-number of compete compel is 2856;]
	now to-number of compete compel is 2704;

a goodrhyme rule (this is the vc-sheet-shell rule):
	if player is not in compete compel, unavailable;
	if sco-sheet-shell is true:
		vcal "You already established a sheet shell here.";
		already-done;
	ready;

this is the vr-sheet-shell rule:
	now sco-sheet-shell is true;
	say "Yes, [if sco-feet-fell is true]the Wheat Well could use a Sheet Shell[else]wherever you go, a Sheet Shell would be useful[end if].";
	kick-off-bell;
	now to-number of compete compel is 2704;

a goodrhyme rule (this is the vc-feet-fell rule):
	if player is not in compete compel, unavailable;
	if sco-wheat-well is false:
		vcp "But you haven't envisioned any place to fall to!";
		not-yet;
	if sco-feet-fell is true:
		vcal "You already slid to a more peaceful space!";
		already-done;
	ready;

this is the vr-feet-fell rule:
	now sco-feet-fell is true;
	say "Oops! With a bit of dream logic, you slip out of where you feel you need to show you are better than someone and to where you can reflect more.";
	now from-number of compete compel is 2754;
	now to-number of compete compel is 2755;
	kick-off-bell;

a goodrhyme rule (this is the vc-beat-bell rule):
	if player is not in compete compel, unavailable;
	if sco-sheet-shell is false:
		vcp "You don't have any (door)bell to beat, yet. [if sco-feet-fell is false]You need to be somewhere more homey[else]Perhaps one will ring soon enough[end if].";
		not-yet;
	if sco-beat-bell is true:
		vcal "You already managed to beat the bell when you needed to!";
		already-done;
	ready;

this is the vh-beet-meat-end rule:
	say "You already got food like that for the feast. You need a verb here.";

this is the vr-beat-bell rule:
	now sco-beat-bell is true;
	say "Aaand ... pursuant to general dream/fantasy logic rules, you just make it! But now, what with the Internet taking over with emails, you often forget what your lovable mail carrier's name is! There must be a clue to who they are. As for how they got there, and how they know you're there, you'll just chalk it up to increased efficiency in the postal service due to progress in the ordinary sort of magic that everyone looks down on but would be terribly sorry if it vanished.";
	now to-number of compete compel is 2703;
	now to-number of sheet shell is 2703;

a goodrhyme rule (this is the vc-meet-mel rule):
	if player is not in compete compel, unavailable;
	if sco-beat-bell is false:
		vcp "Nobody named Mel might be around, yet!";
		not-yet;
	if sco-meet-mel is true:
		vcal "You already met Mel!";
		already-done;
	ready;

this is the vr-meet-mel rule:
	now sco-meet-mel is true;
	say "Yes! Mel the Mail Maven! It has a certain ring to it. You take Mel's delivery. Mel, with a busy route ahead, salutes you and moves on.[paragraph break][pete pell]. Memories come back to you, of Leet Lel, a larger-than-life figure that showed you so much, but who also seemed to have scorn for you. It was hard to tell.";
	now player has pete pell;
	now to-number of compete compel is 8264;
	now to-number of sheet shell is 8264;

a goodrhyme rule (this is the vc-heat-hell rule):
	now eet-ell is -1;
	abide by the try-final-point rule;
	ready;

this is the vr-heat-hell rule:
	now sco-heat-hell is true;
	say "True, Leet Lel showed you a lot of things that maybe nobody else would show you. But it was always tied up with Lel pointing out your inadequacies, or your shortcomings, or how you should really have noted in the 1st place, without really showing you how to figure these things out for yourself.[paragraph break]And maybe the reason others wouldn't show you is, you were a bit too captivated by Lel. Lel isolated you a bit.[paragraph break]But things leaked out. Others had stories you didn't believe at first. Sometimes, you were just glad that you didn't have Leet Lel's wrath to put up with, or sarcasm. Other times you felt guilt that you wanted more. You also felt as though you were subsisting at times. You felt guilty saying you liked the company of less exciting people, and you even felt reaching out to them left them with the Mark of the Bore.[paragraph break]Leet Lel did a lot for you. But it came with strings attached. Stirrings beyond just being grateful, or reciprocity. You recall now other people warning you about Leet Lel, but Leet Lel showed you so much at first. Then, less, with time.[paragraph break]Maybe you will remember other awful things, or clues you should have seen, noticing a detail here or there. You hope you do not dwell on it, but when it comes up, you won't block it out. Perhaps there are ways you still need to find to let go, and the only way to start is to admit you still have frustration to let go of. A lot of it.[paragraph break]You also think of other people less abrasive than Leet Lel you were genuinely sad to see depart, or those who were less abrasive, and you said 'If I can put up with Leet Lel, I better put up with them.' But you did not really need to.[paragraph break]You will think about Leet Lel when you need to, and it will help you put aside things in the present.";
	end-stub;

a goodrhyme rule (this is the vc-repeat-repel rule):
	now eet-ell is 0;
	abide by the try-final-point rule;
	ready;

this is the vr-repeat-repel rule:
	now sco-repeat-repel is true;
	say "Leet Lel left a complex legacy. You can't deny the practical help they gave, or the cool songs or movies or books they showed you, but it came with a price. However, things just about balanced out. For all Leet Lel's thoughtlessness and condescension, there was a lot. And yet. There were times Leet Lel would be upset you knew something they didn't.[paragraph break]Behind every good thing they showed you was an insistence you give them all the credit you could, mentally or publicly. They'd often switch between askikng you to blow off their quirks and wanting you to pay more attention. For all they showed you, sometimes you just wished they'd not shove their oar in. But sometimes that motivated you well.[paragraph break]Taking the good with the bad is a cliche, but you feel you don't need to trumpet how Leet Lel could be a total jerk, and you don't need to feel too indebted for the neat things they showed you.[paragraph break]You do know there were times Leet Lel could be thoughtless and cruel when it seemed easier not to, and there were times Leet Lel would surprise you with something neat, just because. The two were not necessarily bound together, and you don't need to ruminate any more about someone who always said to live in the moment. You hope to do more of the second with your acquaintances remaining in this world.";
	end-stub;

a goodrhyme rule (this is the vc-sweet-swell rule):
	now eet-ell is 1;
	abide by the try-final-point rule;
	ready;

this is the vh-suite-swell rule:
	say "You don't need to gush about your current living conditions. You need to reflect on the past, though perhaps you will find wonderful things there.";

this is the vr-sweet-swell rule:
	now sco-sweet-swell is true;
	say "You decide that, although Leet Lel could be pretty crude at times, they showed you so much, and you really can't put that aside. After all, you didn't focus on the bad stuff, and they weren't exactly bigoted. You realize you were able to take the good stuff from them and leave the bad parts of their personality, yet, at the same time, you realize they were a bit braver than you, and you could have used even more of that bravery. Your overall evaluation is favorable. After all, how many inventors and such were jerks who left great work for non-jerks to enjoy? You were lucky to have had Leet Lel as part of your life.";
	end-stub;

book general flip stubs

chapter Reeve Row

to rhyme-rug-check (nu - a number):
	declue-thing-by rayed rug and nu;
	say "[line break]A ray vanishes from the rug!";
	if rug-score is 3:
		if sco-heave-ho is false:
			say "The rug sits uselessly in the center of the floor, now. It looks weird there. Perhaps you could find a way to move it.";
	else if rug-score is 2:
		say "The rug's rays go from one-third dull to one-third bright.";
	else:
		say "Several rays on the rug (you'd say one-third) grow duller.";
	general-rug-check;

to oven-check:
	if oven-fix-score is 3:
		say "With the plug, wires and dials, and with help from the goon guide's technical section, you replace the faulty parts of the oven[if sco-prune-pride is false]. The technical advice is great. Maybe there is some non-technical stuff, too[end if].";
		now oven-fixed-yet is true;
		repeat with OT running through oventhings:
			moot OT;

this is the self rhymes check rule:
	if player is not in reeve row:
		vcp "That seems worth doing, but this isn't the right place.";
		not-yet;

chapter Squalor Square

to say a-o:
	say "[one of]A burly workman named Amped Ox takes you and the downed dork to the Damped Docks[or]Amped Ox drops by again, leading you and the downed dork to a new area of the Damped Docks[stopping]"

this is the bloat-transform rule:
	if player does not have meat moat and player does not have beet boat and player does not have neat note and bleat bloat is not touchable, unavailable;
	if sco-neat-note is false:
		vcp "Perhaps ... but unfortunately, the bleat-bloat distracts you from finding fun new stuff. Perhaps a helpful guide, or guiding document, would be the thing.";
		not-yet;

this is the be-square rule:
	if player is not in squalor square:
		vcp "The neat note is a map of Squalor Square. You should probably go there and try again.";
		not-yet;

to check-dork-done:
	say "[line break]";
	if square-food-score is 2:
		now to-number of neat note is 2704;
	if dork-score is 1:
		say "The downed dork mumbles something about not wanting to leave you this quick, but also not hanging around if they're not wanted. You're in no rush, and you wonder what other secrets Squalor Square holds.";
	else if dork-score is 2:
		say "The downed dork asks if they should stay or go. Your instincts are to stay. Maybe there's one more thing here.";
	if dork-score is 3:
		say "The downed dork looks around. 'Anything else?'[paragraph break]Squalor Square seems pretty empty. You thank them heartily. They say, well, it was nothing, really, and they are sure more with-it people could've found things quicker.[paragraph break]It's tough. You've never done this before, but you sit the downed dork down and explain that, well, if they are good at something, they are good at something, and perhaps there is a reason they are good at something. Perhaps caring about things others find dorky is worthwhile. You've been there. You don't want to be all 'don't let it bother you' or 'cool is a state of mind' but you can't deny the dork's profound help in, well, finding weird stuff you could not have on your own. You apologize if you were too didactic before suggesting they resolve to show a bit of confidence beyond 'turn that frown upside down.' The dork agrees and thanks you.[paragraph break]There is one last thing. You need to put the neat note where the next person who needs to navigate the area can find it. The downed dork has a surprisingly good suggestion[paragraph break]Your work together done, you part ways.";
		moot downed dork;
		moot neat note;

chapter Ooh Ooh

to say zl:
	say "[one of]Zuzu and Loulou[or]Loulou and Zuzu[at random]"

to process-cocoa (nu - a number):
	say "[line break]";
	if ooh-score is 7:
		now to-number of ooh ooh is -3;
		now to-number of loulou is -4;
		now to-number of zuzu is -4;
	else if ooh-score <= 4:
		decrease to-number of ooh ooh by nu;
		decrease to-number of zuzu by nu;
		decrease to-number of loulou by nu;
	else:
		increase to-number of ooh ooh by nu;
		increase to-number of zuzu by nu;
		increase to-number of loulou by nu;
	if ooh-score is 4:
		now to-number of ooh ooh is 0 - to-number of ooh ooh;
		now to-number of zuzu is 0 - to-number of zuzu;
		now to-number of loulou is 0 - to-number of loulou;
	if ooh-score is 1:
		say "[zl] seem to warm up to you.";
	else if ooh-score is 2:
		say "[zl] warm up to you further.";
	else if ooh-score is 3:
		say "[zl] discuss among themselves, pointing at you. After some chatter, they both hold up one finger. ";
	else if ooh-score is 4:
		say "[zl] pound you on the back. You sure have some great ideas in line with their thinking! After some awkward hand gestures they indicate they don't mind if you stay or go. You've earned this packet of cocoa.";
		now player has cocoa;
		repeat through table of verb checks:
			if there is no best-room entry, next;
			if best-room entry is not ooh ooh, next;
			if check-rule entry is vc-massive-mitt rule, next;
			if idid entry is false:
				now core entry is false;
	else if ooh-score is 5:
		say "[zl] look even happier now. Dare they expect more solidarity?";
	else if ooh-score is 6:
		say "[zl] are starting to look exhausted from all this imagining.";
	else if ooh-score is 7:
		say "[zl] wipe their brows after this latest feat of imagination. Nothing more to do here, really.";
	else:
		say "BUG. This should not be reached, but [ooh-score] should between 1 and 7.";

chapter Trappy Trawl

this is the call-a-pal rule:
	if player is in trappy trawl:
		vcp "That might be someone to call once you are in the main ritual room.";
		not-yet;
	if player is not in happy hall, unavailable;

this is the ritual-stuff rule:
	if player is in trappy trawl:
		vcp "That might be an interesting part of the ritual once you make it to the main ritual room.";
		not-yet;
	if player is not in happy hall, unavailable;

chapter Happy Hall

this is the trawl-not-hall rule:
	if player is in happy hall:
		vcal "You already navigated the trappy trawl.";
		already-done;
	if player is not in trappy trawl, unavailable;

to reduce-hall-if:
	if to-number of happy hall is 5508, now to-number of happy hall is 2754; [paul/saul goes to caul then yall then ball, which are the same number, which saves coding]

to hall-move (rh - a rhymeperson):
	move rh to Happy Hall;
	if rh is female:
		set pronoun her to rh;
		if to-number of own aura is 0:
			say "[line break]The hall's own aura pulses, then gets lighter.";
			now to-number of own aura is -4;
	else:
		set pronoun him to rh;
	if other-guy of rh is in Happy Hall:
		say "[line break][rh] and [other-guy of rh] acknowledge each other with a nod. But they don't get too close.";

chapter Compete Compel

this is the try-final-point rule:
	if player is not in compete compel, unavailable;
	if well-score < 5:
		vcp "You haven't reflected enough for [if eet-ell is 0]a balanced[else if eet-ell < 0]an angry[else if eet-ell > 0]a favorable[end if] judgement, yet.";
		not-yet;
	if pete pell is not examined:
		vcp "You get a sense such a judgment would be more appropriate once you've examined the book you just got.";
		not-yet;
	ready;

to kick-off-bell:
	if well-score is 3:
		move Sheet Shell to Compete Compel;
		say "[line break]Suddenly, with the Sheet Shell in place, ready for a book, you hear your postal delivery person in the distance. You hope to rush to the door, wherever that is. You know if you don't show up, they won't leave any potential package. But how? You need motivation and hustle!"

to end-stub:
	if allow-win is false:
		say "In testing, so we try again.";
		continue the action;
	follow the score and thinking changes rule;
	force-status;
	end the story finally saying "Cease seeking: Peace? PEAKING!";
	follow the shutdown rules;

to say final-judge-think of (nu - a number):
	say "[if leet lel is examined]You read up on Leet Lel, so you can conclude[else]You need a story to reflect on before concluding[end if] [b][if nu is -1]HEAT HELL[else if nu is 1]SWEET SWELL[else]REPEAT REPEL[end if][r]".

volume can't go

table of noways
noway-rm	noway-txt
Reeve Row	"[if sco-leave-lo is false]You need to find a way out. Yet you can't just go out ... you need to pump yourself up to get out, somehow[else][reeve-row-nogo][end if]."
Wandering Where	"[if noun is planar]You feel you should be able to go [noun]. [planar-where][else]You can really only wander in the four cardinal directions[craw-foreshadow][end if]."

to say reeve-row-nogo:
	say "It's not a very big house. ";
	if sco-heave-ho is false:
		say "You've suspected there was a passage down, but you see nothing at the moment";
	else if sco-believe-below is false:
		say "You suspected the old passage-under-the-rug trick, but you just don't have faith it's REALLY there";
	else:
		say "You can go down, but you can't really explore this small house. You don't need to";
	say ". There's always back [b]OUT[r]";
	if copper key is moot, say ", and you unlocked a passage [b]DOWN[r], too"

to say craw-foreshadow:
	if noun is not down:
		if chrome craw is in wandering where:
			say " or down into the chrome craw[if slurry-score is 3], though you're done there[end if]";
		continue the action;
	if sco-chrome-craw is false:
		choose row with check-rule of vc-chrome-craw rule in table of verb checks;
		if think-cue entry is true:
			if sides-visited is 3:
				say ", and you should probably activate the chrome craw now";
			else:
				say ", but you don't have the confidence to find the chrome craw, yet";
		else:
			say ", though actually, there is a way to see what's down later";

to say planar-where:
	say "[if number of wanderable directions is 0]You need to guess at where the roads might lead[else]You already were able to make your way [list of wanderable directions], after all[end if]"

table of noways (continued)
noway-rm	noway-txt
Dove n Doubt	"Perhaps there are secret passages for the mafia-connected, but not for you."
Gap Goo	"You can only really go back north. You don't want to know how far the goo gap extends."
Ooh Ooh	"[if goon guide is not examined]It's too misty to see far[else]The passive pit blocks all directions except to the south[end if]."
Squalor Square	"Oh no. You don't want to go deeper into this mess."

table of noways (continued)
noway-rm	noway-txt
Stuck State	"Perhaps finding what you need will get you out of here. You can't see a passage out."
Took Tarred	"You can't leave until you've found everything the shook shard offers."
Toy Toss	"There's a reward, and [boy boss] and the Joy Jaws won't let you leave until you pick the right one."
Hi Ho	"No, there must be something you can buy on the cheap."
Edgy Ill Hedge-y Hill	"[if noun is north]You discover the 'Leet Lark, It' Meat Market. But you have enough meat[else if noun is south]The Greeter Grill Sweeter Swill serves all sorts of non-nutritious, but delicious stuff. Not quite, though[else if noun is east]The 'Vim? VERY Dim' Dairy is mis-titled on purpose, but you don't need dairy products[else if noun is west]Linky Lakes Stinky Steaks goes the 'ironic title' route, but it also goes the 'if you have to ask, you can't afford' route[end if]. You sense you're missing a food group, with the directions you tried."
Woe Worry Slow Slurry	"Only more worries are beyond, you suspect. [if slurry-score is 3]You've got enough side dishes[else if slurry-score is 0]Surely, you can finagle something from the denizens here[else]There's enough more food to pick off here, anyway[end if]."

table of noways (continued)
noway-rm	noway-txt
Trappy Trawl	"You crash around into some walls, which make noises like WHOMP and WHUMP, which don't sound quite right."
Happy Hall	"You can't walk out on the ritual now!"
Compete Compel	"This is the final lair of solitude. You've completed a quest for your peers. Now you will sit here and sort things out."

volume homonyms

table of thing homonyms
mything	hom-rule (a rule)	myhom (topic)	custom-msg (text)
rayed rug	vh-raid-rug rule	"raid"	--

this is the vh-raid-rug rule:
	say "You ";
	if rug-score is 3:
		say "raided the rug enough, so to speak, to get the mug, jug and plug";
	else:
		say "will need to raid the rug[if rug-score > 0] some more[end if] to find what you need, but not with homonyms"

table of room homonyms
loc	hom-rule (a rule)	myhom (topic)	custom-msg (text)
dove n doubt	vh-love-shove rule		"loving/shoving"	--
sty sto	vh-sty-sto rule	"eye/oh/stow/high/hoe"	--

this is the vh-love-shove rule:
	say "No, here [if oven-score is 0]will be[else if oven-score is 1]should still be[else]was[end if] the place for slight colloquialisms."

this is the vh-sty-sto rule:
	say "You need a slightly colloquial food that rhymes with the shop. No homonyms."

volume irregular lump solutions

table of irregular rhymes [ugh! Not the solution I wanted, but CUCKOO necessitates something odd and not just zapping the space.]
check-rule	solution
vc-muumuu rule	"muumuu"
vc-doodoo rule	"doodoo"
vc-poohpooh rule	"poohpooh"
vc-tutu rule	"tutu"
vc-juju rule	"juju"
vc-cuckoo rule	"cuckoo"
vc-booboo rule	"booboo"
vc-potpourri rule	"potpourri"

Beef Beans Grief Greens Tables ends here.

---- DOCUMENTATION ----
