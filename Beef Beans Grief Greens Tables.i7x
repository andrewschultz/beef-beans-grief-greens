Version 1/240113 of Beef Beans Grief Greens Tables by Andrew Schultz begins here.

"This lays out the major tables for Beef Beans Grief Greens for easy indexing and searching, as well as any rules related to the tables. It's mostly for tracking points."

volume the main thing

table of verb checks
w1 (text)	w2 (text)	posthom (topic)	hom-txt-rule (rule)	think-cue	okflip	core	idid	best-room	check-rule	run-rule	wfull (topic)	think-advice (text)
"sassed"	"ceased"	--	--	false	true	false	false	--	vc-sassed-ceased rule	vr-sassed-ceased rule	--	--
"made"	"mug"	--	--	false	true	true	false	reeve row	vc-made-mug rule	vr-made-mug rule	--	--
"jade"	"jug"	--	--	false	true	true	false	reeve row	vc-jade-jug rule	vr-jade-jug rule	--	--
"played"	"plug"	--	--	false	true	true	false	reeve row	vc-played-plug rule	vr-played-plug rule	--	--
"heave"	"ho"	--	--	false	true	true	false	reeve row	vc-heave-ho rule	vr-heave-ho rule	--	--
"leave"	"lo"	--	--	false	true	true	false	reeve row	vc-leave-lo rule	vr-leave-lo rule	--	--
"believe"	"below"	--	--	false	true	true	false	reeve row	vc-believe-below rule	vr-believe-below rule	--	"You can [b]BELIEVE BELOW[r] [once-now of vc-believe-below rule] you see a possible way down from [here-in of reeve row]."
"grieve"	"grow"	--	--	false	true	true	false	reeve row	vc-grieve-grow rule	vr-grieve-grow rule	--	--
"prune"	"pride"	--	--	false	true	true	false	reeve row	vc-prune-pride rule	vr-prune-pride rule	--	--
"pondering"	"pair"	--	--	false	true	true	false	wandering where	vc-pondering-pair rule	vr-pondering-pair rule	--	--
"squandering"	"square"	--	--	false	true	true	false	wandering where	vc-squandering-square rule	vr-squandering-square rule	--	--
"maundering"	"mare"	--	--	false	true	true	false	wandering where	vc-maundering-mare rule	vr-maundering-mare rule	--	--
"laundering"	"lair"	--	--	false	true	true	false	wandering where	vc-laundering-lair rule	vr-laundering-lair rule	--	--
"flopper"	"flea"	--	--	false	true	true	false	wandering where	vc-flopper-flea rule	vr-flopper-flea rule	--	--
"massive"	"mitt"	--	--	false	true	true	false	oh oh	vc-massive-mitt rule	vr-massive-mitt rule	--	--
"co"	"coa"	--	--	false	true	true	false	oh oh	vc-co-coa rule	vr-co-coa rule	"cocoa"	--
"fight"	"fires"	--	--	false	true	true	false	squalor square	vc-fight-fires rule	vr-fight-fires rule	--	--
"white"	"wires"	--	--	false	true	true	false	squalor square	vc-white-wires rule	vr-white-wires rule	--	--
"lovin"	"lout"	--	--	false	true	true	false	dove n doubt	vc-lovin-lout rule	vr-lovin-lout rule	--	--
"shovin"	"shout"	--	--	false	true	true	false	dove n doubt	vc-shovin-shout rule	vr-shovin-shout rule	--	--
"oven"	"out"	--	--	false	true	true	false	dove n doubt	vc-oven-out rule	vr-oven-out rule	--	--
"pluck"	"plate"	--	--	false	true	true	false	stuck state	vc-pluck-plate rule	vr-pluck-plate rule	--	--
"scrappy"	"scrawl"	--	--	false	true	true	false	trappy trawl	vc-scrappy-scrawl rule	vr-scrappy-scrawl rule	--	--
"whappy"	"wall"	--	--	false	true	true	false	trappy trawl	vc-whappy-wall rule	vr-whappy-wall rule	--	--
"lone"	"laura"	--	--	false	true	true	false	happy hall	vc-lone-laura rule	vr-lone-laura rule	--	--
"known"	"nora"	--	--	false	true	true	false	happy hall	vc-known-nora rule	vr-known-nora rule	--	--
"pappy"	"paul"	--	--	false	true	true	false	happy hall	vc-pappy-paul rule	vr-pappy-paul rule	--	--
"sappy"	"saul"	--	--	false	true	true	false	happy hall	vc-sappy-saul rule	vr-sappy-saul rule	--	--
"cappy"	"caul"	--	--	false	true	true	false	happy hall	vc-cappy-caul rule	vr-cappy-caul rule	--	--
"yappy"	"yall"	--	--	false	true	true	false	happy hall	vc-yappy-yall rule	vr-yappy-yall rule	--	--
"bappy"	"ball"	--	--	false	true	true	false	happy hall	vc-bappy-ball rule	vr-bappy-ball rule	--	--
"wheat"	"well"	--	--	false	true	true	false	compete compel	vc-wheat-well rule	vr-wheat-well rule	--	--
"sheet"	"shell"	--	--	false	true	true	false	compete compel	vc-sheet-shell rule	vr-sheet-shell rule	--	--
"feet"	"fell"	--	--	false	true	true	false	compete compel	vc-feet-fell rule	vr-feet-fell rule	--	--
"beat"	"bell"	--	--	false	true	true	false	compete compel	vc-beat-bell rule	vr-beat-bell rule	--	--
"meet"	"mel"	--	--	false	true	true	false	compete compel	vc-meet-mel rule	vr-meet-mel rule	--	--
"sweet"	"swell"	--	--	false	true	true	false	compete compel	vc-sweet-swell rule	vr-sweet-swell rule	--	--
"heat"	"hell"	--	--	false	true	true	false	compete compel	vc-heat-hell rule	vr-heat-hell rule	--	--
"repeat"	"repel"	--	--	false	true	true	false	compete compel	vc-repeat-repel rule	vr-repeat-repel rule	--	--
"zap"	"zoo"	--	--	false	true	true	false	Gap Goo	vc-zap-zoo rule	vr-zap-zoo rule	--	--
"notice"	"knife"	--	--	false	true	true	false	Gap Goo	vc-notice-knife rule	vr-notice-knife rule	--	--
"carrot"	"cake"	--	--	false	true	true	false	Gap Goo	vc-carrot-cake rule	vr-carrot-cake rule	--	--
"see"	"soup"	--	--	false	true	true	false	gap goo	vc-see-soup rule	vr-see-soup rule	--	--
"dented"	"dials"	--	--	false	true	true	false	gap goo	vc-dented-dials rule	vr-dented-dials rule	--	--

section goon guide

a goodrhyme rule (this is the vc-prune-pride rule):
	if goon guide is not touchable, unavailable;
	if sco-prune-pride is true:
		vcal "You're already humble enough.";
		already-done;
	ready;

this is the vr-prune-pride rule:
	now sco-prune-pride is true;
	say "You get over your rage at the goon guide allowing bad people to make food as tasty as good people could make. You realize you are not the judge.[paragraph break]The goon guide will be there with further technical help when you need it, to help repair the oven. You have an idea of the exact parts that are missing now. You will be more observant in the future when you look around.";
	move passive pit to Oh Oh;
	move vented vials to Gap Goo;
	move Light Lyres to Squalor Square;


chapter all the time

a goodrhyme rule (this is the vc-sassed-ceased rule):
	if player does not have fast feast, unavailable;
	if sco-sassed-ceased is true:
		vcal "You already felt relief things settled down!";
		already-done;
	ready;

this is the vr-sassed-ceased rule:
	now sco-sassed-ceased is true;
	say "Yes, you're glad you were given this responsibility, and people snickered about how flaky you were.";

chapter reeve row scoring

section rayed rug flips

a goodrhyme rule (this is the vc-made-mug rule):
	if rayed rug is not touchable, unavailable;
	if sco-made-mug is true:
		vcal "You already have a made/maid mug for the meal.";
		already-done;
	ready;

this is the vr-made-mug rule:
	now sco-made-mug is true;
	say "A made mug apppears. It will be useful for the meal.";
	now player has made mug;
	rug-check;

a goodrhyme rule (this is the vc-jade-jug rule):
	if rayed rug is not touchable, unavailable;
	if sco-jade-jug is true:
		vcal "You already have a jade jug for the meal.";
		already-done;
	ready;

this is the vr-jade-jug rule:
	now sco-jade-jug is true;
	say "A jade jug appears. It will be useful for the meal.";
	now player has jade jug;
	rug-check;

a goodrhyme rule (this is the vc-played-plug rule):
	if rayed rug is not touchable, unavailable;
	if sco-played-plug is true:
		vcal "You already got a played plug!";
		already-done;
	ready;

this is the vr-played-plug rule:
	now sco-played-plug is true;
	say "Ah, that's what the bump in the rayed rug is.";
	now player has played plug;

section reeve row general flips

a goodrhyme rule (this is the vc-believe-below rule):
	if player is not in reeve row, unavailable;
	if sco-believe-below is true:
		vcal "You already believed in[if sco-heave-ho is true], and found,[end if] a passage down.";
		already-done;
	ready;

this is the vr-believe-below rule:
	if sco-heave-ho is false:
		say "You believe something must be below. Below that rug, you'd guess. You might not be able to remove it by yourself. But you have faith now!";
	else:
		say "Aha! You wondered if something was below the rug. Now you see the outline of a trap door beneath the rug."; [?? if rug there and you believed, you'd like to remove it]
	now sco-believe-below is true;
	trigger-bee;

a goodrhyme rule (this is the vc-leave-lo rule):
	if player is not in reeve row, unavailable;
	ready;

this is the vr-leave-lo rule:
	if sco-leave-lo is false:
		say "Sometimes you just need the right time to say a simple phrase to get you going. And you do, here.";
		open-psg outside and wandering where;
	else:
		say "[i][bracket]You can just go [b]OUT[r][i] or [b]N/NORTH[r][i] for fewer keystrokes.[close bracket]";
	now sco-leave-lo is true;
	move player to Wandering Where;

a goodrhyme rule (this is the vc-heave-ho rule):
	if player is not in reeve row, unavailable;
	if sco-heave-ho is true:
		vcal "You already pulled the rug away!";
		already-done;
	if sco-oven-out is false:
		vcp "You try to pull the rayed rug, but it feels glued down. You can't move it by yourself!";
		not-yet;
	ready;

this is the vr-heave-ho rule:
	now sco-heave-ho is true;
	say "You and [the lout] pull on one, two, THREE! It takes a couple of times, but the rug bursts free from the floor. You see [if sco-believe-below is true]nothing under the rug. You were sort of hoping for a new passage. Maybe you can do more than just hope.[paragraph break]Maybe there's a way to visualize a way down[else]an outline of the passage you believed was there[end if].[paragraph break]You shake hands with [the lout], who exits and nods to wish you the best.";
	moot lout;
	say "[line break]Also, you take some time to look inside the oven. You find something called a goon guide. But you can't use it, because you're not a goon. Or can you?";
	trigger-bee;

a goodrhyme rule (this is the vc-grieve-grow rule):
	if player is not in reeve row, unavailable;
	if sco-grieve-grow is true:
		vcal "Too much grief will undo the growing you did.";
		already-done;
	ready;

this is the vr-grieve-grow rule:
	now sco-grieve-grow is true;
	say "You reflect on things. You recognize too much of this is bad. All the same, it puts you in a mindset to do what you must do.";

chapter wandering where scoring

a goodrhyme rule (this is the vc-pondering-pair rule):
	if player is not in wandering where, unavailable;
	ready;

this is the vr-pondering-pair rule:
	now sco-pondering-pair is true;
	wander oh oh;

a goodrhyme rule (this is the vc-squandering-square rule):
	if player is not in wandering where, unavailable;
	ready;

this is the vr-squandering-square rule:
	now sco-squandering-square is true;
	wander squalor square;

a goodrhyme rule (this is the vc-maundering-mare rule):
	if player is not in wandering where, unavailable;
	ready;

this is the vr-maundering-mare rule:
	now sco-maundering-mare is true;
	wander Gap Goo;

a goodrhyme rule (this is the vc-laundering-lair rule):
	if player is not in wandering where, unavailable;
	ready;

this is the vr-laundering-lair rule:
	now sco-laundering-lair is true;
	wander Dove N Doubt;

section bopper bee scoring

a goodrhyme rule (this is the vc-flopper-flea rule):
	if copper key is not touchable and bopper bee is not touchable, unavailable;
	if sco-flopper-flea is true:
		vcal "You already got rid of the bopper bee!";
		already-done;
	ready;

this is the vr-flopper-flea rule:
	now sco-flopper-flea is true;
	say "Hooray! You figured what to do! You get a point!";

chapter oh oh scoring

a goodrhyme rule (this is the vc-co-coa rule):
	if player is not in oh oh, unavailable;
	if sco-cocoa is true:
		vcal "You already got cocoa!";
		already-done;
	ready;

this is the vr-co-coa rule:
	now sco-cocoa is true;
	say "Bobo and Jojo found your little skirmish heartwarming. They're glad to help maybe warm you later. You enjoy a cup of cocoa, and they give you some powdered cocoa for later.";

section passive pit

a goodrhyme rule (this is the vc-massive-mitt rule):
	if player is not in oh oh, unavailable;
	if sco-massive-mitt is false:
		vcp "You still need to do something!";
		not-yet;
	if sco-massive-mitt is true:
		vcal "You already did this!";
		already-done;
	ready;

this is the vr-massive-mitt rule:
	now sco-massive-mitt is true;
	say "Hooray! You figured what to do! You get a point!";

section squalor square scoring

a goodrhyme rule (this is the vc-fight-fires rule):
	if player is not in squalor square, unavailable;
	if sco-fight-fires is false:
		vcp "You still need to do something!";
		not-yet;
	if sco-fight-fires is true:
		vcal "You already fought the fires!";
		already-done;
	ready;

this is the vr-fight-fires rule:
	now sco-fight-fires is true;
	say "Hooray! You figured what to do! You get a point!";

a goodrhyme rule (this is the vc-white-wires rule):
	if light lyres are not touchable, unavailable;
	if sco-fight-fires is false:
		vcp "You can't see anything that might have white wires! Well, anything safe to handle!";
		not-yet;
	if sco-white-wires is true:
		vcal "You already got the white wires!";
		already-done;
	ready;

this is the vr-white-wires rule:
	now sco-white-wires is true;
	say "You see what to look for in the lyres. The white wires have to come in handy to fix the oven.";

chapter Dove N Doubt scoring

a goodrhyme rule (this is the vc-lovin-lout rule):
	if player is not in dove n doubt, unavailable;
	if sco-lovin-lout is true:
		vcal "The lovin['] lout already appeared to help you!";
		already-done;
	ready;

this is the vr-lovin-lout rule:
	now sco-lovin-lout is true;
	say "A lovin['] lout strolls sheepishly into view, not sure to explain whether they've been terribly loyal, or they didn't mean to do the stuff the bad people suggested they do, or else. You assure them that's over now, and you want someone who can help a bit. For what, you're not sure yet.[paragraph break]You take a closer look at the oven. It's missing a lot! You probably need to find bits and pieces to make it serviceable again.";
	move oven to Dove N Doubt;

a goodrhyme rule (this is the vc-shovin-shout rule):
	if player is not in dove n doubt, unavailable;
	if sco-lovin-lout is false:
		vcp "A shout won't be enough to move the oven on your own.";
		not-yet;
	if sco-shovin-shout is true:
		vcal "You already shouted with the lout! It's time to [if sco-oven-out is true]look elsewhere[else]finish removing the oven[end if].";
		already-done;
	ready;

this is the vr-shovin-shout rule:
	now sco-shovin-shout is true;
	say "You and the lovin['] lout get together and push the oven forward. Something seems to crumble loose. There must be just one more thing to do.";

a goodrhyme rule (this is the vc-oven-out rule):
	if player is not in dove n doubt and oven is not touchable, unavailable;
	if sco-shovin-shout is false:
		vcp "You may need to dislodge the oven a bit before really moving it.";
		not-yet;
	if sco-oven-out is true:
		vcal "But you've already moved the oven back to Reeve Row!";
		already-done;
	ready;

this is the vr-oven-out rule:
	now sco-oven-out is true;
	say "With the right commands and pacing, you and [the lout] move the oven from its former position. The lout, very unloutishly indeed, asks if you need the oven to move anywhere. You defer all 'Oh, it'd be too much!' but after some nonsense back-and-forthing, you both realize that's wasted energy. You both move the oven back to Reeve Row pretty quickly. The oven is -- well, you sense it isn't ready to cook anything, but it's a lot easier to repair here.[paragraph break]The [lout] waits around, as if they suspect they could help you with one more thing.";
	move oven to Reeve Row;
	move lovin lout to Reeve Row;
	move player to Reeve Row, without printing a room description;

chapter Gap Goo scoring

a goodrhyme rule (this is the vc-zap-zoo rule):
	if player is not in gap goo, unavailable;
	if sco-zap-zoo is true:
		vcal "But you already converted the goo to a zoo!";
		already-done;
	ready;

this is the vr-zap-zoo rule:
	now sco-zap-zoo is true;
	say "Yes, yes. Much better now. The terrain changes!";
	move ferret fake to gap goo;

section items in Gap Goo

a goodrhyme rule (this is the vc-notice-knife rule):
	if lotus life is not touchable, unavailable;
	if sco-notice-knife is true:
		vcal "You already noticed the knife! There is nothing more.";
		already-done;
	ready;

this is the vr-notice-knife rule:
	now sco-notice-knife is true;
	say "Ah! Yes! The lotus life almost took over parts of the zoo. You had to look for the knife, but it is there. And once you do, you notice a whole bunch of knives. Wow, when this zoo blossoms, it blossoms.";
	now player has knives;

a goodrhyme rule (this is the vc-see-soup rule):
	if tree troop is not touchable, unavailable;
	if sco-see-soup is true:
		vcal "You already got the soup!";
		already-done;
	ready;

this is the vr-see-soup rule:
	now sco-see-soup is true;
	say "You nod to the tree troop. You see what you want. They are glad to give it to you.";
	now player has soup;

a goodrhyme rule (this is the vc-carrot-cake rule):
	if ferret fake is not touchable, unavailable;
	ready;

this is the vr-carrot-cake rule:
	now sco-carrot-cake is true;
	say "Splat! The Ferret, Fake runs off to reveal an untouched carrot cake!";
	now player has carrot cake;

a goodrhyme rule (this is the vc-dented-dials rule):
	if player is not in gap goo, unavailable;
	if sco-dented-dials is false:
		vcp "You still need to do something!";
		not-yet;
	if sco-dented-dials is true:
		vcal "You already did this!";
		already-done;
	ready;

this is the vr-dented-dials rule:
	now sco-dented-dials is true;
	say "Hooray! You figured what to do! You get a point!";

chapter stuck state scoring

a goodrhyme rule (this is the vc-pluck-plate rule):
	if player is not in stuck state, unavailable;
	ready;

this is the vr-pluck-plate rule:
	now sco-pluck-plate is true;
	say "You find one plate, and it helps you out of your slump, and you find a bunch! You walk out of the fog to ...";
	now player has plates;
	move player to wandering where, without printing a room description;

chapter Trappy Trawl scoring

a goodrhyme rule (this is the vc-scrappy-scrawl rule):
	abide by the trawl-not-hall rule;
	if sco-scrappy-scrawl is true:
		vcal "You already found the scrappy scrawl!";
		already-done;
	ready;

this is the vr-scrappy-scrawl rule:
	now sco-scrappy-scrawl is true;
	say "Ah! You had confidence you could find scrappy scrawl, and you do. The instructions seem to make sense. You need to look to break open a passage where there is none, yet. An unexpectedly hollow place. But how?";

a goodrhyme rule (this is the vc-whappy-wall rule):
	abide by the trawl-not-hall rule;
	if sco-scrappy-scrawl is false:
		vcp "But you have no indication where a wall might sound whappy. Well, not yet.";
		not-yet;
	ready;

this is the vr-whappy-wall rule:
	now sco-whappy-wall is true;
	say "Whunk. Whunk. The wall makes the same noise, until ... WHAP! There it is! You tap at it a bit, and a passage opens up to ...";
	move player to Happy Hall;

chapter happy hall scoring

section room proper

a goodrhyme rule (this is the vc-pappy-paul rule):
	if player is not in happy hall, unavailable;
	if sco-pappy-paul is true:
		vcal "You already summoned Pappy Paul!";
		already-done;
	ready;

this is the vr-pappy-paul rule:
	now sco-pappy-paul is true;
	say "Pappy Paul appears when summoned.";
	move Pappy Paul to Happy Hall;

a goodrhyme rule (this is the vc-sappy-saul rule):
	if player is not in happy hall, unavailable;
	if sco-sappy-saul is true:
		vcal "You already summoned Sappy Saul!";
		already-done;
	ready;

this is the vr-sappy-saul rule:
	now sco-sappy-saul is true;
	say "Sappy Saul appears when summoned.";
	move Sappy Saul to Happy Hall;

a goodrhyme rule (this is the vc-cappy-caul rule):
	if player is not in happy hall, unavailable;
	if sco-cappy-caul is true:
		vcal "You already retrieved your ceremonial garb!";
		already-done;
	if hall-guest-score < 4:
		vcp "You can't wear the ceremonial gear until everyone's here!";
		not-yet;
	ready;

this is the vr-cappy-caul rule:
	now sco-cappy-caul is true;
	say "You retrieved your ceremonial garb. For the first rite, you sacrifice [feast]. There are other copies of it.[paragraph break]Now is the time to stimulate discussion.";
	moot fast feast;
	if sco-sassed-ceased is false, max-down;

a goodrhyme rule (this is the vc-yappy-yall rule):
	if player is not in happy hall, unavailable;
	if sco-cappy-caul is false:
		vcp "You need to retrieve your ceremonial garb!";
		not-yet;
	if sco-yappy-yall is true:
		vcal "You already held a discussion. Now it is time for games.";
		already-done;
	ready;

this is the vr-yappy-yall rule:
	now sco-yappy-yall is true;
	say "Everyone has a say about the deceased.!";

a goodrhyme rule (this is the vc-bappy-ball rule):
	if player is not in happy hall, unavailable;
	if sco-yappy-yall is false:
		vcp "First, discussion.";
		not-yet;
	ready;

this is the vr-bappy-ball rule:
	now sco-bappy-ball is true;
	say "A long game of bappy-ball is played. Everyone is exhausted and hungry afterwards. They eat everything that is put in front of them. You doze off, and you are given a vision... one where you are forced to be more, or less, popular than your associate. Yes, 'associate' is the right word.";
	move player to Compete Compel

section own aura

a goodrhyme rule (this is the vc-lone-laura rule):
	if player is not in happy hall, unavailable;
	if sco-lone-laura is true:
		vcal "You already summoned Lone Laura!";
		already-done;
	ready;

this is the vr-lone-laura rule:
	now sco-lone-laura is true;
	say "Lone Laura appears!";

a goodrhyme rule (this is the vc-known-nora rule):
	if player is not in happy hall, unavailable;
	if sco-known-nora is true:
		vcal "You already summoned Known Nora!";
		already-done;
	ready;

this is the vr-known-nora rule:
	now sco-known-nora is true;
	say "Known Nora appears!";

chapter Compete Compel scoring

a goodrhyme rule (this is the vc-wheat-well rule):
	if player is not in compete compel, unavailable;
	if sco-wheat-well is true:
		vcal "You already [if sco-feet-fell is true]got here[else]figured your destination[end if]!";
		already-done;
	ready;

this is the vr-wheat-well rule:
	now sco-wheat-well is true;
	say "Yes, the wheat well must be your destination. Now how to stumble across to it?";

a goodrhyme rule (this is the vc-sheet-shell rule):
	if player is not in compete compel, unavailable;
	if sco-sheet-shell is true:
		vcal "You already know the need for a sheet shell.";
		already-done;
	ready;

this is the vr-sheet-shell rule:
	now sco-sheet-shell is true;
	say "Yes, [if sco-feet-fell is true]the Wheat Well could use a Sheet Shell[else]wherever you go, a Sheet Shell would be useful[end if].";
	kick-off-bell;

a goodrhyme rule (this is the vc-feet-fell rule):
	if player is not in compete compel, unavailable;
	if sco-wheat-well is false:
		vcp "But you don't have any place to fall to!";
		not-yet;
	if sco-feet-fell is true:
		vcal "You already slide to a more peaceful space!";
		already-done;
	ready;

this is the vr-feet-fell rule:
	now sco-feet-fell is true;
	say "Oops! With a bit of dream logic, you slip out of where you feel you need to show you are better than someone and to where you can reflect more.";
	kick-off-bell;

a goodrhyme rule (this is the vc-beat-bell rule):
	if player is not in compete compel, unavailable;
	if sco-feet-fell is false:
		vcp "You don't have any bell to beat, yet. You need to be somewhere more homey.";
		not-yet;
	if sco-beat-bell is true:
		vcal "You already committed to go fast!";
		already-done;
	ready;

this is the vr-beat-bell rule:
	now sco-beat-bell is true;
	say "Aaand ... pursuant to the rules of dream logic, you just make it! But now you have no clue what your mail carrier's name is!";

a goodrhyme rule (this is the vc-meet-mel rule):
	if player is not in compete compel, unavailable;
	if sco-beat-bell is false:
		vcp "Nobody named Mel might be around, yet!";
		not-yet;
	if sco-meet-mel is true:
		vcal "You already met Mel!";
		already-done;
	ready;

this is the vr-meet-mel rule:
	now sco-meet-mel is true;
	say "Yes! You go out to meet Mel! Mel gives you something to read.";

a goodrhyme rule (this is the vc-sweet-swell rule):
	if player is not in compete compel, unavailable;
	if well-score < 5:
		vcp "You haven't reflected enough for a thrilling judgement, yet.";
		not-yet;
	ready;

this is the vr-sweet-swell rule:
	now sco-sweet-swell is true;
	say "Leet Lel was pretty awesome.";

a goodrhyme rule (this is the vc-heat-hell rule):
	if player is not in compete compel, unavailable;
	if well-score < 5:
		vcp "You haven't reflected enough for an angry judgement, yet.";
		not-yet;
	ready;

this is the vr-heat-hell rule:
	now sco-heat-hell is true;
	say "Leet Lel was pretty awful.";

a goodrhyme rule (this is the vc-repeat-repel rule):
	if player is not in compete compel, unavailable;
	if well-score < 5:
		vcp "You haven't reflected enough for a balanced judgement, yet.";
		not-yet;
	ready;

this is the vr-repeat-repel rule:
	now sco-repeat-repel is true;
	say "Leet Lel wasn't so bad.";

book general flip stubs

chapter Reeve Row

to rug-check:
	say "[line break]A ray vanishes from the rug. [if rug-score is 3]There are no more. It stays uselessly off to the side[else]Perhaps the rug offers more[end if]";

to trigger-bee:
	if sco-heave-ho is true and sco-believe-below is true:
		say "You hear an odd, insistent buzzing from outside.";
		move bopper bee to Wandering Where;

chapter Trappy Trawl

this is the trawl-not-hall rule:
	if player is in happy hall:
		vcal "You already navigated the trappy trawl.";
		already-done;
	if player is not in trappy trawl, unavailable;

chapter Compete Compel

to kick-off-bell:
	if well-score is 3:
		move Sheet Shell to Compete Compel;
		say "[line break]Suddenly, with the Sheet Shell in place, ready for a book, you hear your postal delivery person in the distance. You hope to rush to the door, wherever that is. You know if you don't show up, they won't leave any potential package. But how? You need motivation and hustle!"

volume can't go

table of noways
noway-rm	noway-txt
Reeve Row	"Weird. No directions here."

volume homonyms

table of thing homonyms
mything	hom-rule (a rule)	myhom (topic)	custom-msg (text)
(a thing)	--	--	--

table of room homonyms
loc	hom-rule (a rule)	myhom (topic)	custom-msg (text)
dove n doubt	vh-love-shove rule		"loving/shoving"	--

this is the vh-love-shove rule:
	say "No, here [if oven-score is 0]will be[else if oven-score is 1]should still be[else]was[end if] the place for slight colloquialisms."


Beef Beans Grief Greens Tables ends here.

---- DOCUMENTATION ----
